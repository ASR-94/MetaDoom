actor KeenSpawner replaces CommanderKeen
{
	Health 0x7fffffff
	Height 0
	Radius 0
	MONSTER
	-COUNTKILL
	-SOLID
	+NOCLIP
	+NOTDMATCH
	+SPAWNCEILING 
	States
	{
	Spawn:
		KEEN A 0
		KEEN A 0 A_Jump(256,"Keen","Budo","Dave")
	Keen:
		TNT1 A 0 A_SpawnItemEx("MetaKeen",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION|SXF_TRANSFERAMBUSHFLAG)
		goto CreepyStalker
	Budo:
		TNT1 A 0 A_SpawnItemEx("MetaBudo",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION|SXF_TRANSFERAMBUSHFLAG)
		goto CreepyStalker
	Dave:
		TNT1 A 0 A_SpawnItemEx("MetaDave",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION|SXF_TRANSFERAMBUSHFLAG)
		goto CreepyStalker
	CreepyStalker:
		TNT1 A 1 // SFX: HEAVY BREATHING
		loop
	Death:
		TNT1 A 1
		TNT1 A 1 A_KeenDie
		TNT1 A -1
		Stop
	}
}

ACTOR MetaKeen
{
	Health 100
	Radius 16
	Height 72
	Mass 10000000
	PainChance 256
	+SOLID 
	+SPAWNCEILING 
	+NOGRAVITY 
	+SHOOTABLE 
	+COUNTKILL 
	+NOICEDEATH
	+ISMONSTER
	PainSound "keens/keen/hit"
	DeathSound "keens/keen/die"
	damagefactor HolyWater, 0
	damagefactor FireExtinguisher, 0
	damagefactor DogCollar, 0
	painchance DogCollar, 0
	States
	{
	Spawn:
		KEEN A   -1
		Loop
	Death:
		KEEN Z 0 A_GiveToTarget("ScoreItem", 666)
		KEEN Z 0 A_Scream
		KEEN Z 0 A_Fall
		KEEN Z 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		KEEN Z	-1 A_KillMaster //A_KeenDie
		Stop
	Pain:
		KEEN M	4
		KEEN M	8 A_Pain
		Goto Spawn
	}
}

ACTOR MetaBudo : MetaKeen
{
	PainSound "keens/budo/hit"
	DeathSound "keens/budo/die"
	States
	{
	Spawn:
		KBUD A -1
		Loop
	Pain:
		KBUD A	4
		KBUD A	8 A_Pain
		Goto Spawn
	}
}

ACTOR MetaDave : MetaKeen
{
	PainSound "keens/dave/hit"
	DeathSound "keens/dave/die"
	States
	{
	Spawn:
		KDAV A -1
		Loop
	Pain:
		KDAV A	4
		KDAV A	8 A_Pain
		Goto Spawn
	}
}