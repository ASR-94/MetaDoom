/*
----------------------------
MetaDoom
ZScript Definitions
Lore Item Spawner
----------------------------
*/

// This handles the potential spawning of lore items in secret rooms.
// It should be about a 10% chance in secret sectors that have room for such
// things to feasibly appear in (ie. not a closed door like half the fecking
// secret sectors in Doom E1...
class MetaLoreItems : EventHandler
{
	override void WorldLoaded(WorldEvent e)
	{
		bool metamap = false;
		
		BlockThingsIterator it = BlockThingsIterator.CreateFromPos(0,0,0,0,32767,0);
        while (it.Next())
        {
            Actor mo = it.thing;
            if (mo is "MetaDoomMapID")
			{
				if (meta_developer)
					Console.printf("Lore Item: This is a MetaDoom map, snuff the lore item spawning!");
				metamap = true;
			}
		}
		
		for (int i = 0; i < level.Sectors.Size(); i++)
		{
			// Grab the center point of all sectors in the map.
			let c = level.Sectors[i].centerspot;
			Vector3 spam3 = (c.x, c.y, level.Sectors[i].floorplane.ZAtPoint(c));
			
			// Is the selected sector a secret, and is its center point actually within playable space?
			if (level.IsPointInMap(spam3) && level.Sectors[i].IsSecret())
			{
				if (metamap == false && meta_lootspawns == 1)
				{
					// Do an RNG roll - have lore items only have a 10% chance of spawning.
					int lorerng = random(0,100);
					
					if (lorerng <= 10)
					{
						// Make sure the secret isn't a closed door like in E1M1, so the item has room to spawn.
						if (level.Sectors[i].floorplane.ZAtPoint(c) != level.Sectors[i].ceilingplane.ZAtPoint(c))
						{
							Actor.Spawn("MetaLoreItemSpawner", spam3);
							if (meta_developer)
								Console.printf("Lore Item: Spawned at %i %i", c.x, c.y);
						} else {
							if (meta_developer)
								Console.printf("Lore Item: Failed to spawn at %i %i", c.x, c.y);
						}
					} else {
						if (meta_developer)
								Console.printf("Lore Items: Failed RNG Roll (%i)", lorerng);
					}
				}
			}
		}
	}
}