// One ugly work around to add item pickup sounds without
// defining new ammo types, coming right up!

ACTOR Clip2 : Clip replaces Clip
{
	Game Doom
	SpawnID 11
	Inventory.PickupMessage "$GOTCLIP"
	Inventory.Amount 10
	Inventory.MaxAmount 200
	Ammo.BackpackAmount 10
	Ammo.BackpackMaxAmount 400
	TeleFogSourceType MetaItemFog
	TeleFogDestType MetaItemFog
	Inventory.Icon "CLIPA0"
	inventory.pickupsound "items/clip"
	States
	{
	Spawn:
		CLIP A -1
		Stop
	}
}

// Clip Boxes have a 25% chance of spawning a Dog Collar on spawn.
ACTOR ClipBox2 : Clip replaces ClipBox
{
	Game Doom
	SpawnID 139
	Inventory.PickupMessage "$GOTCLIPBOX"
	Inventory.Amount 50
	inventory.pickupsound "items/clipx"
	TeleFogSourceType MetaItemFog
	TeleFogDestType MetaItemFog
	States
	{
	Spawn:
		AMMO A 0
		AMMO A 0 A_Jump(64, "SpawnInv")
		goto Chill
	SpawnInv:
		AMMO A 0 A_Jump(256, "DogCollar")
		goto Chill
	DogCollar:
		AMMO A 0 A_SpawnItemEx("DogCollarPickup",0,0,0,random(4,16),random(4,16),0,random(0,360))
		goto Chill
	Chill:
		AMMO A -1
		Stop
	}
}

// Rockets have a 33% chance of spawning a Hand Grenade on spawn.
ACTOR RocketAmmo2 : RocketAmmo replaces RocketAmmo
{
	Game Doom
	SpawnID 140
	Inventory.PickupMessage "$GOTROCKET"
	Inventory.Amount 1
	Inventory.MaxAmount 50
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 100
	Inventory.Icon "ROCKA0"
	inventory.pickupsound "items/rocket"
	TeleFogSourceType MetaItemFog
	TeleFogDestType MetaItemFog
	States
	{
	Spawn:
		ROCK A 0
		ROCK A 0 A_Jump(86, "SpawnInv")
		goto Chill
	SpawnInv:
		ROCK A 0 A_Jump(256, "HandGrenade")
		goto Chill
	HandGrenade:
		ROCK A 0 A_SpawnItemEx("HandGrenadePickup",0,0,0,random(4,16),random(4,16),0,random(0,360))
		goto Chill
	Chill:
		ROCK A -1
		Stop
	}
}

// Rocket Boxes have a 37% chance of spawning a Hand Grenade on spawn.
ACTOR RocketBox2 : RocketAmmo replaces RocketBox
{
	Game Doom
	SpawnID 141
	Inventory.PickupMessage "$GOTROCKBOX"
	Inventory.Amount 5
	inventory.pickupsound "items/rocketx"
	TeleFogSourceType MetaItemFog
	TeleFogDestType MetaItemFog
	States
	{
	Spawn:
		BROK A 0
		BROK A 0 A_Jump(96, "SpawnInv")
		goto Chill
	SpawnInv:
		BROK A 0 A_Jump(256, "HandGrenade")
		goto Chill
	HandGrenade:
		BROK A 0 A_SpawnItemEx("HandGrenadePickup",0,0,0,random(4,16),random(4,16),0,random(0,360))
		goto Chill
	Chill:
		BROK A -1
		Stop
	}
}

// Cells have a 25% chance of spawning either a Siphon Grenade or a Tesla Rocket.
ACTOR Cell2 : Cell replaces Cell
{
	Game Doom
	SpawnID 75
	Inventory.PickupMessage "$GOTCELL"
	Inventory.Amount 20
	Inventory.MaxAmount 300
	Ammo.BackpackAmount 20
	Ammo.BackpackMaxAmount 600
	Inventory.Icon "CELLA0"
	inventory.pickupsound "items/cell"
	TeleFogSourceType MetaItemFog
	TeleFogDestType MetaItemFog
	States
	{
	Spawn:
		CELL A 0
		CELL A 0 A_Jump(64, "SpawnInv")
		goto Chill
	SpawnInv:
		BROK A 0 A_Jump(128, "Siphon")
		BROK A 0 A_Jump(256, "Tesla")
		goto Chill
	Tesla:
		BROK A 0 A_SpawnItemEx("TeslaRocketPickup",0,0,0,random(4,16),random(4,16),0,random(0,360))
		goto Chill	
	Siphon:
		BROK A 0 A_SpawnItemEx("SiphonGrenadePickup",0,0,0,random(4,16),random(4,16),0,random(0,360))
		goto Chill	
	Chill:
		CELL A -1
		Stop
	}
}

// Cell Packs have a 25% chance of spawning BFG ammo alongside it. If this happens,
// there is an additional 50% chance of a Tesla Rocket also spawning.
ACTOR CellPack2 : Cell replaces CellPack
{
	Game Doom
	SpawnID 142
	Inventory.PickupMessage "$GOTCELLBOX"
	Inventory.Amount 100 
	inventory.pickupsound "items/cellx"
	TeleFogSourceType MetaItemFog
	TeleFogDestType MetaItemFog
	States
	{
	Spawn:
		CELP A 0
		CELP A 0 A_Jump(64, "SpawnInv")
		goto Chill
	SpawnInv:
		BROK A 0 A_Jump(256, "BFGAmmo")
		goto Chill
	BFGAmmo:
		BROK A 0 A_SpawnItemEx("BFGAmmo",0,0,0,random(4,16),random(4,16),0,random(0,360))
		BROK A 0 A_Jump(128, "Tesla")
		goto Chill	
	Tesla:
		BROK A 0 A_SpawnItemEx("TeslaRocketPickup",0,0,0,random(4,16),random(4,16),0,random(0,360))
		goto Chill	
	Chill:
		CELP A -1
		Stop
	}
}

ACTOR Shell2 : Shell replaces Shell
{
	Game Doom
	SpawnID 12
	Inventory.PickupMessage "$GOTSHELLS"
	Inventory.Amount 4
	Inventory.MaxAmount 50
	Ammo.BackpackAmount 4
	Ammo.BackpackMaxAmount 100
	Inventory.Icon "SHELA0"
	inventory.pickupsound "items/shell"
	TeleFogSourceType MetaItemFog
	TeleFogDestType MetaItemFog
	States
	{
	Spawn:
		SHEL A -1
		Stop
	}
}

ACTOR ShellBox2 : Shell replaces ShellBox
{
	Game Doom
	SpawnID 143
	Inventory.PickupMessage "$GOTSHELLBOX"
	inventory.pickupsound "items/shellx"
	Inventory.Amount 20
	TeleFogSourceType MetaItemFog
	TeleFogDestType MetaItemFog
	States
	{
	Spawn:
		SBOX A -1
		Stop
	}
}

// Backpacks have a 25% chance of spawning a Kinetic Mine.
// We use a seperate actor to spawn the backpack, ala the weapons, because otherwise
// the Kinetic Mine-spawning logic would trigger a crash in the Codex screen.
actor BackpackSpawner replaces Backpack
{
	Game Doom
	SpawnID 144
	TeleFogSourceType MetaItemFog
	TeleFogDestType MetaItemFog
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(64,"Mines")
		goto Backpack
	Mines:
		TNT1 A 0 A_SpawnItemEx("KineticMinePickup",0,0,0,random(4,16),random(4,16),0,random(0,360))
		goto Backpack
	Backpack:
		TNT1 A 0 A_SpawnItemEx("Backpack2",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERSPECIAL|SXF_NOCHECKPOSITION)
		stop
	}
}

ACTOR Backpack2 : BackpackItem //replaces Backpack
{
	Height 26
	Inventory.PickupMessage "$GOTBACKPACK"
    inventory.pickupsound "items/pack"
	TeleFogSourceType MetaItemFog
	TeleFogDestType MetaItemFog
	States
	{
	Spawn:
		BPAK A -1
		Stop
	}
}

// New Ammos
ACTOR BFGAmmo : Ammo
{
	Inventory.PickupMessage "$GOTARGENT"
	Inventory.Amount 1
	Inventory.MaxAmount 10
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10
	Inventory.Icon "ARGAA0"
	inventory.pickupsound "items/bfg"
	TeleFogSourceType MetaItemFog
	TeleFogDestType MetaItemFog
	scale 0.75
	+BLOCKASPLAYER
	States
	{
	Spawn:
		ARGA AB 4
		loop
	}
}

ACTOR SoulAmmo : Ammo
{
	Inventory.PickupMessage "$GOTSOUL"
	Inventory.Amount 1
	Inventory.MaxAmount 10
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10
	Inventory.Icon "SOLAA0"
	inventory.pickupsound "weapons/soulcube/ammo"
	TeleFogSourceType MetaItemFog
	TeleFogDestType MetaItemFog
	renderstyle Add
	scale 1
	scale 0.5
	States
	{
	Spawn:
		SOLA ABCB 4 bright
		loop
	}
}

// Lazy(?) way of ensuring that souls only spawn 50% of the time when killing enemies.
actor SoulSpawner
{
	radius 16
	height 16
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(CallACS("Item_SoulCube", 0, 0, 0) == 2, "GimmeTheSouls")
		stop
	GimmeTheSouls:
		TNT1 A 0 A_Jump(128, "NoReallyGiveMeTheSouls")
		stop
	NoReallyGiveMeTheSouls:
		TNT1 A 0 A_SpawnItemEx("SoulAmmo",0,0,0,random(0,8),random(0,8),8,random(0,360))
		stop
	}
}