/*
----------------------------
MetaDoom
Actor Definitions
Health Pickups
----------------------------
*/

// Health bonuses occasionally spawn loot. See loot.dec
ACTOR MetaHealthBonus : Health replaces HealthBonus
{
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.Amount 1
	Inventory.MaxAmount 200
	Inventory.PickupMessage "$GOTHTHBONUS"
	Inventory.PickupSound "items/health/shard"
	TeleFogSourceType MetaItemFog
	TeleFogDestType MetaItemFog
	States
	{
	Spawn:
		BON1 A 0
		BON1 A 0 A_JumpIf(GetCvar(meta_lootspawns) == 0, 2)
		BON1 A 0 A_SpawnItem("LootSpawner",random(32,64),0)
		BON1 A 0
	DoALoop:
		BON1 ABCDCB 4
		Loop
	}
}

ACTOR MetaStimpack : Health replaces Stimpack
{
	Inventory.Amount 10
	Inventory.PickupMessage "$GOTSTIM"
	Inventory.PickupSound "items/health/stim"
	TeleFogSourceType MetaItemFog
	TeleFogDestType MetaItemFog
	States
	{
	Spawn:
		STIM A -1
		Stop
	}
}

// Medikits have a 6.25% chance of spawning a Shield Wall
ACTOR MetaMedikit : Health replaces Medikit
{
	Inventory.Amount 25
	Inventory.PickupMessage "$GOTMEDIKIT"
	Health.LowMessage 25, "$GOTMEDINEED"
	Inventory.PickupSound "items/health/medi"
	TeleFogSourceType MetaItemFog
	TeleFogDestType MetaItemFog
	Scale 0.5
	States
	{
	Spawn:
		MEDI A 0
		MEDI A 0 A_Jump(16, "SpawnInv")
		goto Chill
	SpawnInv:
		MEDI A 0 A_Jump(256, "ShieldWall")
		goto Chill
	ShieldWall:
		MEDI A 0 A_SpawnItemEx("ShieldWallPickup", random(-64, 64), random(-64, 64), 0)
		goto Chill
	Chill:
		MEDI A -1
		Stop
	}
}

// Alternate Medikit with no item spawn chance, used by Quad Damage
ACTOR MetaMedikitNoItemSpawn : MetaMedikit
{
	States
	{
	Spawn:
		MEDI A 0
		MEDI A 0 
		goto Chill
	}
}