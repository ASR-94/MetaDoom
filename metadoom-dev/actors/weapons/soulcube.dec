actor SoulCubeUsed : Boolean {}
actor SoulCubeMessagePopped : Boolean {}

ACTOR MetaSoulcube : MetaDoomWeapon
{
	Weapon.SelectionOrder 2900
	Obituary "$OB_METASOULCUBE"
	Weapon.AmmoUse1 5
	Weapon.AmmoType1 "SoulAmmo"
	Weapon.AmmoUse2 5
	Weapon.AmmoType2 "SoulAmmo"
	Inventory.Pickupmessage "$GOTSOULCUBE"
	Inventory.PickupSound "weapons/soulcube/get"
	Tag "$TAGSOULCUBE"
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.UNTOSSABLE
	+FLOATBOB
	States
	{
	Ready:
		SOLG AAAAAABBCCDDEEFFGGGGGGHHIIJJKKLLMM 1 
		{
			A_WeaponReady;
			return A_JumpIfInventory("MeleePunch", 1, "MeleePunch");
			
		}
		Loop
	Deselect:
		SOLG A 0
		SOLG A 0 A_JumpIf(health <= 0, "DeadDude")
		SOLG A 0 A_JumpIfInventory("SoulCubeUsed", 1, "FinishDeselect")
		SOLG A 1 Offset(0,33) A_PlaySound("weapons/swap")
		SOLG A 1 Offset(0,35)
		SOLG A 1 Offset(0,39)
		SOLG A 1 Offset(0,44)
		SOLG A 1 Offset(0,49)
		SOLG A 1 Offset(0,55)
		SOLG A 1 Offset(0,60)
		SOLG A 1 Offset(0,70)
		SOLG A 1 Offset(0,80)
		SOLG A 1 Offset(0,90)
	FinishDeselect:
		SOLG A 0 A_TakeInventory("SoulCubeUsed", 9999)
		SOLG A 0 A_Lower
		Loop
	Select:
		SOLG A 1 Offset(0,90) A_PlaySound("weapons/soulcube/use")
		SOLG A 1 Offset(0,80)
		SOLG A 1 Offset(0,70)
		SOLG A 1 Offset(0,60)
		SOLG A 1 Offset(0,55)
		SOLG A 1 Offset(0,49)
		SOLG A 1 Offset(0,44)
		SOLG A 1 Offset(0,39)
		SOLG A 1 Offset(0,35)
		SOLG A 1 Offset(0,33)
		SOLG A 1 Offset(0,32)
		SOLG A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		goto Ready
	AltFire:
	Fire:
		SOLW A 4
		SOLW B 3 bright A_PlaySound("weapons/soulcube/fire", CHAN_WEAPON)
		SOLW CDE 3
		SOLW E 2 Offset(0,35)
		SOLW E 2 Offset(0,45)
		SOLW E 2 Offset(0,65)
		SOLW E 2 Offset(0,85)
		TNT1 A 2 Offset(0,85) A_FireCustomMissile("MetaSoulCubeProj", 0, 1, 0, -16)
		TNT1 A 0 Offset(0,32)
		TNT1 A 0 A_GiveInventory("SoulCubeUsed", 1)
		TNT1 A 0 A_SelectWeapon ("WEAPONTHATDOESNTEXIST!",  SWF_SELECTPRIORITY) // not the grossest hack, but, you know, up there
		Goto Ready
	Flash:
		TNT1 A 4 Bright A_Light1
		Goto LightDone
		TNT1 A 4 Bright A_Light1
		Goto LightDone
 	Spawn:
		SOLP XYZY 10 bright
		loop
	}
}

ACTOR MetaSoulCubeProj
{
	Radius 13
	Height 8
	Speed 35
	Damage (1000)
	damagetype SoulCube
	Projectile
	+RANDOMIZE
	+FLATSPRITE
	+ROLLSPRITE
	+ROLLCENTER
	+SEEKERMISSILE
	+SCREENSEEKER
	RenderStyle Add
	Alpha 1
	Obituary "$OB_METASOULCUBE"
	scale 0.5
	Decal PlasmaScorch
	States
	{
	Spawn:
		SOLP A 0 A_PlaySound ("weapons/soulcube/loop", CHAN_BODY, 1.0, 1)
		SOLP A 0 A_SeekerMissile(0, 90)
		SOLP A 0 A_SpawnItem("SoulCubeTrail1")
		SOLP A 0 A_SpawnItem("SoulCubeTrail2")
		SOLP A 1 Bright
		{
			A_SetRoll(roll + 15);
			A_SetPitch(pitch + 15);
		}
		Loop
	Death:
	Crash:
		TNT1 A 0 A_PlaySound("weapons/soulcube/end")
		TNT1 A 0
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("weapons/soulcube/kill")
		TNT1 A 0
		Stop
	}
}

ACTOR SoulCubeTrail1
{
	+NOBLOCKMAP
	+NOGRAVITY
	RenderStyle Add
	Alpha 1
	scale 0.5
	states
	{
	Spawn:
		SOLT A 2 A_FadeOut
	Finalize:
		SOLT A 2 A_FadeOut
		loop
	}
}
ACTOR SoulCubeTrail2
{
	+NOBLOCKMAP
	+NOGRAVITY
	+FLATSPRITE
	RenderStyle Add
	Alpha 1
	states
	{
	Spawn:
		APBX B 2 A_FadeOut
	Finalize:
		APBX B 2 A_FadeOut
		loop
	}
}