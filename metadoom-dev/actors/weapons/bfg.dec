ACTOR MetaBFG9000Pickup : WeaponGiver
{
	DropItem "MetaBFG9000"
	Inventory.PickupMessage "$GOTBFG9000"
	Inventory.PickupSound "weapons/bfg/get"
	States
	{
	Spawn:
		MBFG Z 0
		MBFG Z 0 A_ChangeFlag ("DROPPED", 0)
		MBFG Z -1
		Stop
	}
}

ACTOR MetaBFG9000 : MetaDoomWeapon
{
	Height 20
	Weapon.SelectionOrder 2800
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 3
	Weapon.AmmoType1 "BFGAmmo"
	Weapon.AmmoType2 "BFGAmmo"
	+WEAPON.NOAUTOFIRE
	Inventory.PickupMessage "$GOTBFG9000"
	Tag "$TAG_BFG9000"
	Inventory.PickupSound "weapons/bfg/get"
	Weapon.BobRangeX 0.5
	Weapon.BobRangey 0.25
	States
	{
	Ready:
		MBFG A 1 A_WeaponReady
		"####" A 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch")
		MBFG B 1 A_WeaponReady
		"####" A 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch")
		Loop
	Deselect:
		MBFG A 1 
		MBFG A 0 A_JumpIf(health <= 0, "DeadDude")
		MBFG A 1 Offset(0,33) A_PlaySound("weapons/swap")
		MBFG A 1 Offset(0,35)
		MBFG A 1 Offset(0,39)
		MBFG A 1 Offset(0,44)
		MBFG A 1 Offset(0,49)
		MBFG A 1 Offset(0,55)
		MBFG A 1 Offset(0,60)
		MBFG A 1 Offset(0,70)
		MBFG A 1 Offset(0,80)
		MBFG A 1 Offset(0,90)
	FinishDeselect:
		MBFG A 0 A_Lower
		Loop
	Select:
		MBFG A 1 Offset(0,90) A_PlaySound("weapons/bfg/use")
		MBFG A 1 Offset(0,80)
		MBFG A 1 Offset(0,70)
		MBFG A 1 Offset(0,60)
		MBFG A 1 Offset(0,55)
		MBFG A 1 Offset(0,49)
		MBFG A 1 Offset(0,44)
		MBFG A 1 Offset(0,39)
		MBFG A 1 Offset(0,35)
		MBFG A 1 Offset(0,33)
		MBFG A 1 Offset(0,32)
		goto Ready
	Fire:
		MBFG A 1 Offset(1,32) A_PlaySound("weapons/bfg/charge")
		MBFG B 1 Offset(-1,32)
		MBFG A 1 Offset(1,32)
		MBFG B 1 Offset(-2,32)
		MBFG A 1 Offset(2,32)
		MBFG B 1 Offset(-2,32)
		MBFG A 1 Offset(3,32)
		MBFG B 1 Offset(-3,32)
		MBFG A 1 Offset(3,32)
		MBFG B 1 Offset(-3,32)
		MBFG A 1 Offset(3,32)
		MBFG B 1 Offset(-3,32)
		MBFG A 1 Offset(4,32)
		MBFG B 1 Offset(-4,32)
		MBFG A 1 Offset(4,32)
		MBFG B 1 Offset(-4,32)
		MBFG A 1 Offset(4,32)
		MBFG B 1 Offset(-4,32)
		MBFG A 1 Offset(4,32)
		MBFG B 1 Offset(-4,32)
		MBFG A 1 Offset(6,32)
		MBFG B 1 Offset(-6,32)
		MBFG A 1 Offset(6,32)
		MBFG B 1 Offset(-6,32)
		MBFG A 1 Offset(6,32)
		MBFG B 1 Offset(-6,32)
		MBFG A 1 Offset(6,32)
		MBFG B 1 Offset(-6,32)
		MBFG A 1 Offset(0,32)
		MBFG C 1 bright
		{
			A_FireCustomMissile("MetaBFGBall");
			A_PlaySound ("weapons/bfg/fire1", CHAN_WEAPON);
			A_GunFlash;
			A_Quake (8,10,0,750,0);
			A_ZoomFactor(0.6, ZOOM_INSTANT|ZOOM_NOSCALETURNING);
		}
		MBFG C 1 bright Offset(0,45) A_SetPitch(pitch-4)
		MBFG D 1 bright Offset(0,48) A_SetPitch(pitch-4)
		MBFG D 1 bright Offset(0,48) A_ZoomFactor(1.0, ZOOM_NOSCALETURNING)
		MBFG E 1 bright Offset(0,46) A_SetPitch(pitch+1)
		MBFG E 1 bright Offset(0,44) A_SetPitch(pitch+1)
		MBFG F 1 Offset(0,42) A_SetPitch(pitch+1)
		MBFG F 1 Offset(0,38) A_SetPitch(pitch+1) 
		MBFG G 1 Offset(0,36) A_SetPitch(pitch+2)
		MBFG G 1 Offset(0,34) A_SetPitch(pitch+2)
		MBFG H 1 Offset(0,33)
		MBFG H 1 Offset(0,32)
		MBFG ABABABABAB 1
		goto Ready
	AltFire:
		MBFG A 1 Offset(1,32) A_PlaySound("weapons/bfg/charge")
		MBFG B 1 Offset(-1,32)
		MBFG A 1 Offset(1,32)
		MBFG B 1 Offset(-2,32)
		MBFG A 1 Offset(2,32)
		MBFG B 1 Offset(-2,32)
		MBFG A 1 Offset(3,32)
		MBFG B 1 Offset(-3,32)
		MBFG A 1 Offset(3,32)
		MBFG B 1 Offset(-3,32)
		MBFG A 1 Offset(3,32)
		MBFG B 1 Offset(-3,32)
		MBFG A 1 Offset(4,32)
		MBFG B 1 Offset(-4,32)
		MBFG A 1 Offset(4,32)
		MBFG B 1 Offset(-4,32)
		MBFG A 1 Offset(4,32)
		MBFG B 1 Offset(-4,32)
		MBFG A 1 Offset(4,32)
		MBFG B 1 Offset(-4,32)
		MBFG A 1 Offset(6,32)
		MBFG B 1 Offset(-6,32)
		MBFG A 1 Offset(6,32)
		MBFG B 1 Offset(-6,32)
		MBFG A 1 Offset(6,32)
		MBFG B 1 Offset(-6,32)
		MBFG A 1 Offset(6,32)
		MBFG B 1 Offset(-6,32)
		MBFG A 1 Offset(0,32)
		MBFG C 0 bright
		{
			A_PlaySound ("weapons/bfg/fire2", CHAN_WEAPON);
			A_GunFlash;
			A_Quake (8,10,0,750,0);
			A_TakeInventory("BFGAmmo",1);
		}
		MBFG CDCDCDCDCDCDCDCDCDCDCDCDCD 1 bright
		{
			A_FireCustomMissile("MetaBFGAltfire",random(-8,8),0,0,0,FPF_NOAUTOAIM,random(-8,8));
			A_ZoomFactor(frandom(0.8,0.85), ZOOM_NOSCALETURNING);
		}
		MBFG C 1 bright Offset(0,45) A_SetPitch(pitch-4)
		MBFG D 1 bright Offset(0,48) A_SetPitch(pitch-4)
		MBFG D 1 bright Offset(0,48) A_ZoomFactor(1.0, ZOOM_NOSCALETURNING)
		MBFG E 1 bright Offset(0,46) A_SetPitch(pitch+1)
		MBFG E 1 bright Offset(0,44) A_SetPitch(pitch+1)
		MBFG F 1 Offset(0,42) A_SetPitch(pitch+1)
		MBFG F 1 Offset(0,38) A_SetPitch(pitch+1) 
		MBFG G 1 Offset(0,36) A_SetPitch(pitch+2)
		MBFG G 1 Offset(0,34) A_SetPitch(pitch+2)
		MBFG H 1 Offset(0,33)
		MBFG H 1 Offset(0,32)
		MBFG ABABABABAB 1
		goto Ready
	Flash:
		TNT1 A 11 Bright A_Light1
		TNT1 B 6 Bright A_Light2
		Goto LightDone
	Spawn:
		MBFG Z -1
		Stop
	}
}

// bfg zapper stuff from argent from xaser, cheers dogg
ACTOR MetaBFGBall
{
	Radius 13
	Height 8
	Speed 10
	Damage 100
	Projectile
	+RANDOMIZE
	RenderStyle Add
	Alpha 0.75
	Obituary "$OB_MPBFG_BOOM"
	decal BFGLightning
	States
	{
	Spawn:
		BFS1 A 0
		BFS1 A 0 A_PlaySound ("weapons/bfg/fly", CHAN_BODY, 1.0, 1)
		BFS1 AAAABBBB 1 Bright 
		{
			A_SetScale(frandom(0.8,1.2),frandom(0.8,1.2));
			A_SpawnItemEx("BFGTrail", frandom(-10,10), frandom(-10,10), frandom(0,20), frandom(-2.5,2.5), frandom(-2.5,2.5), frandom(-2.5,2.5), 0, SXF_CLIENTSIDE, 0);
		}
	Roam:
		BFS1 AAAABBBB 1 Bright 
		{
			A_SetScale(frandom(0.8,1.2),frandom(0.8,1.2));
			A_SpawnItemEx("BFGTrail", frandom(-10,10), frandom(-10,10), frandom(0,20), frandom(-2.5,2.5), frandom(-2.5,2.5), frandom(-2.5,2.5), 0, SXF_CLIENTSIDE, 0);
		}
		BFS1 A 0
		{
			A_CustomMissile("BFGZapper", 8, 0, 0, CMF_AIMDIRECTION | CMF_TRACKOWNER);
		}
		Loop
	Death:
		BFE1 AAAAAAAAAAAAAAAA 0 Bright A_SpawnItemEx("BFGTrail", 0, frandom(-10,10), frandom(-10,10), 0, frandom(-5,5), frandom(-5,5), 0, SXF_CLIENTSIDE, 0)
		BFE1 A 0
		{
			A_PlaySound ("weapons/bfg/explode", CHAN_BODY);
			A_Quake (8,10,0,750,0);
			//A_BFGSpray("BFGSpray", 20, 15, 360, 1024, 32, 0, BFGF_MISSILEORIGIN);
			//A_BFGSpray("BFGSpray", 40, 15, 90, 1024, 32, 0);
			A_BFGSpray("BFGSpray");
		}
		BFE1 ABCDEF 4 Bright
		Stop
	}
}

ACTOR BFGTrail
{
	Renderstyle Add
	Alpha 0.75
	Scale 0.75
	+NOINTERACTION
	States
	{
	Spawn:
		BTRL ABCD 3 Bright
		Stop
	}
}
ACTOR BFGTrailSmall : BFGTrail { Scale 0.5 }

ACTOR BFGZapper
{
	Radius 2
	Height 2
	Speed 0
	MinMissileChance 0
	Projectile
	+LOOKALLAROUND
	+NOINTERACTION
	+ISMONSTER
	+FRIENDLY
	RenderStyle Add
	Obituary "$OB_MPBFG_SPLASH"
	AttackSound "weapons/bfg/spray"
	MissileHeight 0
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_Look
		Stop
	See:
		TNT1 A 1 {
			A_FaceTarget;
			A_PlaySound("weapons/bfg/spray");
			A_CustomRailgun(20, 0, None, None, RGF_SILENT | RGF_NOPIERCING, 1, 32, "BFGSpray", 0, 0, 0, 0, 4, 0, "BFGZapperTrail");
		}
		Goto Death
	Death:
		TNT1 A 0
		{
		if (score > 0)
			{
				A_GiveToTarget ("ScoreItem", score, AAPTR_PLAYER1);
			}
		}
		Stop
	}
}

ACTOR BFGSpray
{
	+NOBLOCKMAP
	+NOGRAVITY
	+BLOODSPLATTER
	+EXTREMEDEATH
	RenderStyle Add
	Alpha 0.75
	States
	{
	Spawn:
		BFE2 A 0 
		BFE2 A 0 A_PlaySound("weapons/bfg/spray")
		BFE2 AAAAAAAAAAAAAAAA         0        A_SpawnItemEx("BFGTrailSmall", frandom(-10,10), frandom(-10,10), frandom(-10,10), frandom(-2.5,2.5), frandom(-5.0,5.0), frandom(-5.0,5.0), 0, SXF_CLIENTSIDE, 0)
		BFE2 AAAAAABBBBBBCCCCCCDDDDDD 1 Bright A_SpawnItemEx("BFGTrailSmall", frandom(-10,10), frandom(-10,10), frandom(-10,10), frandom(-2.5,2.5), frandom(-2.5,2.5), frandom(-2.5,2.5), 0, SXF_CLIENTSIDE, 0)
		Stop
	}
}

ACTOR BFGZapperTrail
{
	Renderstyle Add
	Alpha 0.75
	Scale 0.75
	+NOINTERACTION
	States
	{
	Spawn:
		BFE2 D 2 Bright
		BTRL D 1 Bright
		Stop
	}
}

ACTOR MetaBFGAltfire
{
	Radius 13
	Height 8
	Speed 25
	Damage 15
	BounceType "Classic"
	BounceFactor 1.0
	Projectile
	+RANDOMIZE
	RenderStyle Add
	Alpha 0.75
	Obituary "$OB_MPBFG_MBF"
	Decal GreenPlasmaScorch
	States
	{
	Spawn:
		PLS1 A 0 A_PlaySound ("weapons/plasmarifle/loop", CHAN_BODY, 1.0, 1)
		PLS1 AB 6 Bright 
		Loop
	Death:
		PLS1 A 0 A_PlaySound("weapons/plasmarifle/hit")
		PLS1 CDEFG 4 Bright
		Stop
	}
}