ACTOR MetaLightningGunPickup : WeaponGiver
{
	DropItem "MetaLightningGun"
	Inventory.PickupMessage "$GOTLIGHTNING"
	Inventory.PickupSound "weapons/plasmarifle/get"
	States
	{
	Spawn:
		PLA0 Z 0
		PLA0 Z 0 A_ChangeFlag ("DROPPED", 0)
		PLA0 Z -1
		Stop
	}
}

ACTOR MetaLightningGun : MetaDoomWeapon
{
	Weapon.SelectionOrder 100
	Weapon.AmmoUse 1
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
	Inventory.PickupMessage "$GOTLIGHTNING"
	Tag "$TAGLIGHTNING"
	Inventory.PickupSound "weapons/plasmarifle/get"
	+WEAPON.NOALERT
	States
	{
	Ready:
		PLA0 A 1 A_WeaponReady
		PLA0 A 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch")
		loop
	Deselect:
		PLA0 A 1 
		TNT1 A 0 A_StopSound(CHAN_BODY)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_StopSound(CHAN_6)
		PLA0 A 0 A_JumpIf(health <= 0, "DeadDude")
		PLA0 A 1 Offset(0,33) A_PlaySound("weapons/swap")
		PLA0 A 1 Offset(0,35)
		PLA0 A 1 Offset(0,39)
		PLA0 A 1 Offset(0,44)
		PLA0 A 1 Offset(0,49)
		PLA0 A 1 Offset(0,55)
		PLA0 A 1 Offset(0,60)
		PLA0 A 1 Offset(0,70)
		PLA0 A 1 Offset(0,80)
		PLA0 A 1 Offset(0,90)
	FinishDeselect:
		PLA0 A 0 A_Lower
		Loop
	Select:
		PLA0 A 1 Offset(0,90) A_PlaySound("weapons/plasmarifle/use")
		PLA0 A 1 Offset(0,80)
		PLA0 A 1 Offset(0,70)
		PLA0 A 1 Offset(0,60)
		PLA0 A 1 Offset(0,55)
		PLA0 A 1 Offset(0,49)
		PLA0 A 1 Offset(0,44)
		PLA0 A 1 Offset(0,39)
		PLA0 A 1 Offset(0,35)
		PLA0 A 1 Offset(0,33)
		PLA0 A 1 Offset(0,32)
		goto Ready
	Fire:
		PLA0 B 0
		PLA0 B 1 bright A_PlaySound("weapons/lightninggun/fire",5)
	Hold:
		PLA0 B 1 bright 
		{
			A_PlaySound("weapons/lightninggun/loop",6,1,1);
			A_GunFlash;
			A_RailAttack(10,0,1,Blue,White,RGF_SILENT|RGF_FULLBRIGHT,0,"MetaLightningPuff",0,0,256,35,16.0,0,"LightningTrailSpawner");
			A_AlertMonsters;
			A_WeaponOffset(random(-2,2),random(30,34));
		}
		PLA0 B 1 A_Refire
	FireEnd:
		PLA0 B 1 bright Offset(0,35) A_PlaySound("weapons/lightninggun/stop",6)
		PLA0 B 1 bright Offset(0,34)
		PLA0 C 1 bright Offset(0,33)
		PLA0 C 1 bright Offset(0,32)
		PLA0 A 0 A_ReFire
		goto Ready
	Flash:
		TNT1 A 4 Bright A_Light1
		Goto LightDone
		TNT1 B 4 Bright A_Light1
		Goto LightDone
	Spawn:
		PLA0 Z -1
		Stop
	}
}

actor LightningTrailSpawner
{
	Radius 8
	Height 8
	RenderStyle "Add"
	Alpha 0.75
	scale 0.25
	States
	{
	Spawn:
		PLSE A 0
		PLSE A 0 A_CheckRange(8, "Brrp")
		stop
	Brrp:
		TNT1 A 0 A_SpawnItemEx("LightningTrailParticle",0,random(-8,8),random(-8,8))
		stop
	}
}

actor LightningTrailParticle
{
	Radius 8
	Height 8
	+RANDOMIZE
	RenderStyle "Add"
	Alpha 0.75
	scale 0.25
	States
	{
	Spawn:
		PLSE ABCDE 1 Bright
		Stop
	}
}

ACTOR MetaLightningPuff : MetaBulletPuff
{
	damagetype LightningGun
	Decal BulletChip
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 4 BRIGHT A_SpawnDebris("PistolTracerSpark")
		Stop
	}
}