actor LightningFired : Counter { Inventory.MaxAmount 5 }

ACTOR MetaLightningGunPickup : WeaponGiver
{
	DropItem "MetaLightningGun"
	Inventory.PickupMessage "$GOTLIGHTNING"
	Inventory.PickupSound "weapons/lightninggun/get"
	scale 0.75
	States
	{
	Spawn:
		THUN Z 0
		THUN Z 0 A_ChangeFlag ("DROPPED", 0)
		THUN Z -1
		Stop
	}
}

ACTOR MetaLightningGun : MetaDoomWeapon
{
	Weapon.SelectionOrder 100
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 15
	Weapon.AmmoGive 40
	Weapon.AmmoType1 "Cell"
	Weapon.AmmoType2 "Cell"
	Inventory.PickupMessage "$GOTLIGHTNING"
	Tag "$TAGLIGHTNING"
	Obituary "$OB_LIGHTNING"
	Inventory.PickupSound "weapons/lightninggun/get"
	+WEAPON.NOALERT
	scale 0.75
	States
	{
	Ready:
		THUN A 0 A_PlaySound ("weapons/lightninggun/idle", CHAN_WEAPON, 0.5, 1)
		THUN A 1 A_WeaponReady
		THUN A 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch")
		THUN B 1 A_WeaponReady
		THUN B 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch")
		THUN C 1 A_WeaponReady
		THUN C 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch")
		loop
	Deselect:
		THUN A 1 
		TNT1 A 0 A_StopSound(CHAN_BODY)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_StopSound(CHAN_6)
		THUN A 0 A_JumpIf(health <= 0, "DeadDude")
		THUN A 1 Offset(0,33) A_PlaySound("weapons/swap")
		THUN B 1 Offset(0,35)
		THUN C 1 Offset(0,39)
		THUN A 1 Offset(0,44)
		THUN B 1 Offset(0,49)
		THUN C 1 Offset(0,55)
		THUN A 1 Offset(0,60)
		THUN B 1 Offset(0,70)
		THUN C 1 Offset(0,80)
		THUN A 1 Offset(0,90)
	FinishDeselect:
		THUN A 0 A_Lower
		Loop
	Select:
		THUN A 1 Offset(0,90) A_PlaySound("weapons/lightninggun/use")
		THUN B 1 Offset(0,80)
		THUN C 1 Offset(0,70)
		THUN A 1 Offset(0,60)
		THUN B 1 Offset(0,55)
		THUN C 1 Offset(0,49)
		THUN A 1 Offset(0,44)
		THUN B 1 Offset(0,39)
		THUN C 1 Offset(0,35)
		THUN A 1 Offset(0,33)
		THUN B 1 Offset(0,32)
		goto Ready
	Fire:
		THUN D 0
		THUN DE 1 bright A_PlaySound("weapons/lightninggun/fire",5)
	Hold:
		THUN F 2 bright 
		{
			A_PlaySound("weapons/lightninggun/loop",6,1,1);
			A_GunFlash;
			if (CountInv("LightningFired") >= 5)
			{
				A_RailAttack(12,0,1,Blue,White,RGF_SILENT|RGF_FULLBRIGHT,0,"MetaLightningPuff",0,0,384,35,16.0,0,"LightningTrailSpawner");
				A_TakeInventory("LightningFired",999);
			} else {
				A_RailAttack(12,0,0,Blue,White,RGF_SILENT|RGF_FULLBRIGHT,0,"MetaLightningPuff",0,0,384,35,16.0,0,"LightningTrailSpawner");
				A_GiveInventory("LightningFired",1);
			}
			A_AlertMonsters;
			A_WeaponOffset(random(-2,2),random(30,34), WOF_INTERPOLATE);
		}
		THUN E 0 A_Refire
	FireEnd:
		THUN E 1 bright Offset(0,35) A_PlaySound("weapons/lightninggun/stop",6)
		THUN E 1 bright Offset(0,34)
		THUN D 1 bright Offset(0,33)
		THUN D 1 bright Offset(0,32)
		THUN A 0 A_ReFire
		goto Ready
		
	AltFire:
		THUN A 0 A_JumpIfInventory("Cell",15,"StunShot")
		THUN A 1 Offset(0,34) A_PlaySound("weapons/grenadelauncher/altdeny", CHAN_BODY)
		THUN A 1 Offset(0,33)
		THUN A 1 Offset(0,32)
		THUN A 10
		goto Ready
	StunShot:
		THUN DE 2 bright A_PlaySound("weapons/lightninggun/altfire")
		THUN D 0 A_GunFlash
		THUN F 0 
		{
		A_FireCustomMissile ("MetaLightningStunProj");
		A_GunFlash;
		A_AlertMonsters;
		}
		THUN F 1 bright Offset(0,45) A_SetPitch(pitch-2)
		THUN F 1 bright Offset(0,48) A_SetPitch(pitch-2)
		THUN F 1 bright Offset(0,47)
		THUN E 1 bright Offset(0,46) A_SetPitch(pitch+1)
		THUN E 1 bright Offset(0,44) A_SetPitch(pitch+1)
		THUN E 1 bright Offset(0,42) A_SetPitch(pitch+1)
		THUN D 1 bright Offset(0,38) A_SetPitch(pitch+1) 
		THUN D 1 bright Offset(0,36) //A_PlaySound("weapons/lightninggun/stop")
		THUN D 1 Offset(0,34) 
		THUN D 1 Offset(0,33) 
		THUN A 1 Offset(0,32) 
		THUN A 1 
		THUN A 8
		THUN A 2
		THUN A 0 A_Refire
		Goto Ready	
		
	Flash:
		TNT1 A 4 Bright A_Light1
		Goto LightDone
		TNT1 B 4 Bright A_Light1
		Goto LightDone
	Spawn:
		THUN Z -1
		Stop
	}
}

actor LightningTrailSpawner
{
	Radius 8
	Height 8
	RenderStyle "Add"
	Alpha 0.75
	scale 0.25
	States
	{
	Spawn:
		PLSE A 0
		PLSE A 0 A_CheckRange(8, "Brrp")
		stop
	Brrp:
		TNT1 A 0 A_SpawnItemEx("LightningTrailParticle",0,random(-8,8),random(-8,8))
		stop
	}
}

actor LightningTrailParticle
{
	Radius 8
	Height 8
	+RANDOMIZE
	RenderStyle "Add"
	Alpha 0.75
	scale 0.25
	States
	{
	Spawn:
		PLSE ABCDE 1 Bright
		Stop
	}
}

ACTOR MetaLightningPuff : MetaBulletPuff
{
	damagetype LightningGun
	Decal BulletChip
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 4 BRIGHT A_SpawnDebris("PistolTracerSpark")
		Stop
	}
}

actor MetaLightningStunProj
{
	+CANBOUNCEWATER
	Radius 8
	Height 8
	Speed 30
	Damage 0
	PROJECTILE
	+RANDOMIZE
	-NOGRAVITY
	+FORCERADIUSDMG
	+CAUSEPAIN
	+FORCEPAIN
	+BLOODLESSIMPACT
	//+RIPPER
	damagetype LightningStun
	Obituary "$OB_LIGHTNING"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ThrustThingZ(0,16,0,1)
		goto Flying
	Flying:
		PLSE AB 1
		loop
	Death:
		TNT1 A 0 A_PlaySound("weapons/lightninggun/altboom")
		TNT1 A 0 A_SpawnItem("MetaLightningStunFX")
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Explode((1), 64, !XF_HURTSOURCE, 1, 64)
		Stop
	}
}

ACTOR MetaLightningStunFX
{
	Radius 1
	Height 1
	+NOBLOCKMAP
	-SOLID
	+NOGRAVITY
	//+FORCEXYBILLBOARD
	+ROLLSPRITE
	+FLATSPRITE
	scale 3.0
	renderstyle Add
	States
	{
		Spawn:
			TNT1 A 0
			PLSE ABABABABABABABABABABABABABABABABABABABABABABABABABABABABABABABABABABABABABABABABABAB 1 bright 
			{
				A_SetScale(frandom(2.9,3.1),frandom(2.9,3.1));
				A_SetTranslucent(frandom(0.9,0.8), 1);
				A_SetRoll(random(0,360));
				A_SpawnItem("MetaLightningStunFXZappy",0,0);
				//A_Warp(AAPTR_DEFAULT,0,0,8,0,WARPF_NOCHECKPOSITION|WARPF_TOFLOOR);
			}
		Die:
			PLSE AB 1 bright A_FadeOut(0.25)
			loop
	}
}

ACTOR MetaLightningStunFXZappy
{
	Radius 1
	Height 1
	+NOBLOCKMAP
	-SOLID
	+NOGRAVITY
	+ROLLSPRITE
	+FLATSPRITE
	scale 2.5
	renderstyle Add
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SetRoll(random(0,360))
			TNT1 A 0 A_SetPitch(random(0,360))
		DoAThing:
			PLSE CD 1 bright A_FadeOut(0.5)
			loop
	}
}