/*
----------------------------
MetaDoom
Actor Definitions
Weapon Core
----------------------------

All weapons inherit from MetaDoomWeapon, and hand off to its states
when the player expires. This is used for the first-person death sequences.
*/

Class MetaDoomWeapon : Weapon
{
	Default
	{
		Weapon.Kickback 100;
		Weapon.BobStyle "Smooth";
		//+INVENTORY.ALWAYSPICKUP
		+DONTGIB
	}
  	States
	{
	DeadDude:
		TNT1 A 0 Offset(0,32);
		TNT1 A 0 A_StopSound(CHAN_BODY);
		TNT1 A 0 A_StopSound(CHAN_WEAPON);
		TNT1 A 0 A_StopSound(CHAN_6);
		TNT1 A 0 A_ZoomFactor(1.0, ZOOM_INSTANT);
		TNT1 A 0 A_TakeInventory("PistolFlashlight", 1);
		TNT1 A 0 A_JumpIfInventory("DeathStateGibbed", 1, "DeathGibbed");
	DeathAnim1:
		TNT1 A 0 Offset(0,32);
		TNT1 A 0 A_StopSound(CHAN_BODY);
		TNT1 A 0 A_StopSound(CHAN_WEAPON);
		TNT1 A 0 A_StopSound(CHAN_6);
		DED1 ABC 1;
		DED1 DE 1;
		DED1 FGHIJ 2;
		DED1 J 11;
		DED1 LMNOPQRSTU 1;
		DED1 U -1;
		loop;
	DeathGibbed:
		TNT1 A -1;
		loop;
	MeleePunch:
		PNCH A 0 A_JumpIfInventory("PowerWeaponLevel2", 1, "MeleePunchBerserk");
		PNCH A 0 A_JumpIfInventory("PowerStrength", 1, "MeleePunchBerserk");
		PNCH A 1 
		{
			A_PlaySound("weapons/melee/wiff");
			
			if (CountInv("PowerDamage") > 0)
			{
				A_PlaySound("powerups/quad/use", CHAN_5);
			}
		}
		PNCH BD 1; //PNCH BCDE 1 
		PNCH F 1 A_CustomPunch(5 * random(1, 5), TRUE, CPF_NOTURN,"MetaMeleePuff",80,0,0,"BasicArmorBonus","weapons/melee/hit");
		PNCH GHI 1;
		PNCH I 5;
		PNCH HGFDB 1; //PNCH HGFEDCBA 1
		TNT1 A 0 Offset(0,90) A_TakeInventory("MeleePunch", 999);
		TNT1 A 0 Offset(0,90) A_Jump(256, "Select");
		stop;
	MeleePunchBerserk:
		PNCH A 1 
		{
			A_PlaySound("weapons/melee/wiff");
			
			if (CountInv("PowerDamage") > 0)
			{
				A_PlaySound("powerups/quad/use", CHAN_5);
			}
		}
		PNCH BD 1; //PNCH BCDE 1 
		PNCH F 1 A_CustomPunch(60 * random(1, 3), TRUE, CPF_NOTURN,"MetaBerserkPuff",80,0,0,"BasicArmorBonus","weapons/melee/hit");
		PNCH GHI 1;
		PNCH I 2;
		PNCH HGFDB 1; //PNCH HGFEDCBA 1
		TNT1 A 0 Offset(0,90) A_TakeInventory("MeleePunch", 999);
		TNT1 A 0 Offset(0,90) A_Jump(256, "Select");
		stop;
	}
}

// For Holy Water Pistol
class GimmeWater : CustomInventory
{
	Default
	{
		+COUNTITEM
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.ISHEALTH
		+INVENTORY.ISARMOR
		Inventory.PickupMessage "$GOTMSPHERE";
		Inventory.PickupSound "weapons/waterpistol/ammo";
	}
	States
	{
	Spawn:
		MEGA A 6 BRIGHT;
		Stop;
	Pickup:
		TNT1 A 0 
		{
			player.bonuscount = BONUSADD;
			A_PlaySound("weapons/waterpistol/ammo", CHAN_PICKUP|CHAN_NOPAUSE, 1, false, ATTN_NONE);
			A_Log("$GOTHOLYWATER",1);
			A_GiveInventory("HolyWater", 1);
		}
		Stop;
	}
}	