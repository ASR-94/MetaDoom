/*
----------------------------
MetaDoom
Actor Definitions
Shells and Casings
----------------------------
*/

// Blatantly stolen from Brutal Doom. :dealwithit:

class BulletCasingSpawn : Actor
{
	Default
	{
		Speed 20;
		PROJECTILE;
		+NOCLIP
		+CLIENTSIDEONLY
	}
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 1 A_SpawnProjectile("EmptyBrass",-5,0,random(-80,-100),2|CMF_BADPITCH,random(45,80));
		Stop;
	}
}

class ShotCaseSpawn : BulletCasingSpawn
{
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 1 A_SpawnProjectile("ShotgunCasing",0,0,random(-80,-100),2|CMF_BADPITCH,random(40,60));
		Stop;
	}
}

class SSGCaseSpawner : ShotcaseSpawn
{
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 1 Thing_ChangeTID(0,390);
	Death:
		TNT1 A 1 A_SpawnProjectile("ShotgunCasing",0,0,random(80,100),2|CMF_BADPITCH,random(50,70));
		Stop;
	}
}

class EmptyBrass : Actor
{
	Default
	{
		BounceType "Doom";
		PROJECTILE;
		-NOGRAVITY
		-NOBLOCKMAP
		+NOTELEPORT
		+dontsplash
		//+FORCEXYBILLBOARD
		+CLIENTSIDEONLY
		Radius 2;
		BounceSound "weapons/casing1";
		Speed 6;
		scale 0.2;
		BounceFactor 0.4;
	}

	States
	{
	Spawn:
		PBUL A 0;
		PBUL A 0 A_Jump(255,"Fly1","Fly2","Fly3","Fly4","Fly5","Fly6");
		goto Fly1;
	Bounce:
		goto Spawn;
	Fly1:
		"####" ABCDEFGH 5;
		loop;
	Fly2:
		"####" ABCDEFGH 3;
		loop;
	Fly3:
		"####" ABCDEFGH 2;
		loop;
	Fly4:
		"####" HGFEDCBA 5;
		loop;
	Fly5:
		"####" HGFEDCBA 3;
		loop;
	Fly6:
		"####" HGFEDCBA 2;
		loop;
	Death:
		"####" A 0 A_Jump(255,"Stop1","Stop2","Stop3","Stop4");
		goto Stop1;
	Stop1:
		"####" B 250 A_CheckSight("Vanish");
		loop;
	Stop2:
		"####" D 250 A_CheckSight("Vanish");
		loop;
	Stop3:
		"####" F 250 A_CheckSight("Vanish");
		loop;
	Stop4:
		"####" H 250 A_CheckSight("Vanish");
		loop;
	Vanish:
		TNT1 A 1;
		Stop;
	}
	
	override bool CanCollideWith(Actor other, bool passive)
	{
		return false;
	}
}

class ShotgunCasing : EmptyBrass
{
	Default
	{
		BounceSound "weapons/casing2";
	}
	
	States
	{
	Spawn:
		PSHE A 0;
		PSHE A 0 A_Jump(255,"Fly1","Fly2","Fly3","Fly4","Fly5","Fly6");
		goto Fly1;
	Bounce:
		goto Spawn;
	}
}