actor DemonKey : Counter { Inventory.MaxAmount 3 }

ACTOR MetaUnmaker : MetaDoomWeapon
{
	Weapon.SelectionOrder 1900
	Obituary "$OB_METAPISTOL"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
	Inventory.Pickupmessage "$GOTUNMAKER"
	Inventory.PickupSound "weapons/unmaker/get"
	Tag "$TAGUNMAKER"
	States
	{
	Ready:
		UNMA A 1 A_WeaponReady
		"####" A 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch")
		Loop
	Deselect:
		UNMA A 1
		UNMA A 0 A_JumpIf(health <= 0, "DeadDude")
		UNMA A 1 Offset(0,33) A_PlaySound("weapons/swap")
		UNMA A 1 Offset(0,35)
		UNMA A 1 Offset(0,39)
		UNMA A 1 Offset(0,44)
		UNMA A 1 Offset(0,49)
		UNMA A 1 Offset(0,55)
		UNMA A 1 Offset(0,60)
		UNMA A 1 Offset(0,70)
		UNMA A 1 Offset(0,80)
		UNMA A 1 Offset(0,90)
	FinishDeselect:
		UNMA A 0 A_Lower
		Loop
	Select:
		UNMA A 1 Offset(0,90) A_PlaySound("weapons/unmaker/use")
		UNMA A 1 Offset(0,80)
		UNMA A 1 Offset(0,70)
		UNMA A 1 Offset(0,60)
		UNMA A 1 Offset(0,55)
		UNMA A 1 Offset(0,49)
		UNMA A 1 Offset(0,44)
		UNMA A 1 Offset(0,39)
		UNMA A 1 Offset(0,35)
		UNMA A 1 Offset(0,33)
		UNMA A 1 Offset(0,32)
		UNMA A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		goto Ready
	Fire:
		UNMA A 0
		UNMA A 0 A_JumpIfInventory("DemonKey", 3, "Fire_Keys3")
		UNMA A 0 A_JumpIfInventory("DemonKey", 2, "Fire_Keys2")
		UNMA A 0 A_JumpIfInventory("DemonKey", 1, "Fire_Keys1")
	Fire_Keys0:
		UNMA A 4
		UNMA B 1 bright
		{
			A_FireCustomMissile("UnmakerTracer");
			A_PlaySound("weapons/unmaker/fire", CHAN_WEAPON);
			A_GunFlash;
			A_AlertMonsters;
		}
		UNMA B 1 bright Offset(0,42)
		UNMA A 1 bright Offset(0,38)
		UNMA A 1 Offset(0,35)
		UNMA A 1 Offset(0,32)
		UNMA A 1
		Goto Ready
	Fire_Keys1:
		UNMA A 2
		UNMA B 1 bright
		{
			A_FireCustomMissile("UnmakerTracer");
			A_PlaySound("weapons/unmaker/fire", CHAN_WEAPON);
			A_GunFlash;
			A_AlertMonsters;
		}
		UNMA B 1 bright Offset(0,40)
		UNMA A 1 Offset(0,33)
		UNMA A 1 Offset(0,32)
		Goto Ready
	Fire_Keys2:
		UNMA A 2
		UNMA B 1 bright
		{
			A_FireCustomMissile("UnmakerTracer", -3);
			A_FireCustomMissile("UnmakerTracer", 3, 0);
			A_PlaySound("weapons/unmaker/fire", CHAN_WEAPON);
			A_GunFlash;
			A_AlertMonsters;
		}
		UNMA B 1 bright Offset(0,40)
		UNMA A 1 Offset(0,33)
		UNMA A 1 Offset(0,32)
		Goto Ready
	Fire_Keys3:
		UNMA A 2
		UNMA B 1 bright
		{
			A_FireCustomMissile("UnmakerTracer", -4, 0);
			A_FireCustomMissile("UnmakerTracer", 4, 0);
			A_FireCustomMissile("UnmakerTracer");
			A_PlaySound("weapons/unmaker/fire", CHAN_WEAPON);
			A_GunFlash;
			A_AlertMonsters;
		}
		UNMA B 1 bright Offset(0,40)
		UNMA A 1 Offset(0,33)
		UNMA A 1 Offset(0,32)
		Goto Ready
	AltFire:
		UNMA A 0
	AltFire_Keys0:
		UNMA A 1 Offset(0,34) A_PlaySound("weapons/rocketlauncher/deto", CHAN_BODY)
		UNMA A 1 Offset(0,33)
		UNMA A 1 Offset(0,32) A_TakeInventory("RLDetonator", 1)
		UNMA A 10
		goto Ready
	Flash:
		TNT1 A 4 Bright A_Light1
		Goto LightDone
		TNT1 A 4 Bright A_Light1
		Goto LightDone
 	Spawn:
		UNMA Z -1
		Stop
	}
}

// Level 1
ACTOR UnmakerTracer : FastProjectile
{
	Alpha 1
	RenderStyle Add
	Radius 2
	Height 2
	damage 7
	Speed 75
	PROJECTILE
	+BLOODSPLATTER
	+NOEXTREMEDEATH
	Scale 1
	MissileType UnmakerTracerTrail
	MissileHeight 8
	Decal BulletChip
	Obituary "$OB_METAPISTOL"
	States
	{
	Spawn:
		LASS AB 1 BRIGHT
		Loop
	Death:
	Crash:
		PUFF A 0 A_PlaySound("weapons/pistol/hitwall", CHAN_BODY, frandom(0.5,1.0))
		PUFF A 2 BRIGHT A_SpawnDebris("UnmakerTracerSpark")
		PUFF B 2 BRIGHT A_SetTranslucent(0.9,0)
		PUFF CD 2 BRIGHT
		Stop
	XDeath:
		TNT1 A 3 A_PlaySound("weapons/pistol/hit")
		Stop
	}
}

ACTOR UnmakerTracerTrail
{ 
	Alpha 1
	RenderStyle Add
	Speed 0
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	+CANNOTPUSH
	+NODAMAGETHRUST
	Scale 0.75
	States
	{
	Spawn:
		LASS AB 1 BRIGHT A_FadeOut(0.1)
		Loop
	}
}

ACTOR UnmakerTracerSpark
{
	Health 4
	radius 3
	height 6
	speed .1
	RenderStyle ADD
	Alpha		1
	Scale 0.5
	Mass		0
	translation "224:224=176:176", "192:197=180:191"
	+missile
	+doombounce
	+FLOORCLIP
	+DONTSPLASH
	+NOTELEPORT
	States
	{
		PLSE E 1
		PLSE EEEEE 1  Bright A_SetTranslucent(.8,1)
		PLSE EEEEEE 1  Bright A_SetTranslucent(.6,1)
		PLSE EEEEEEEE 1  Bright A_SetTranslucent(.4,1)
		PLSE EEEEEEEEEE 1 Bright A_SetTranslucent(.2,1)
		Stop
	}
}

actor DemonKeyPickup : CustomInventory
{
	inventory.pickupmessage "$GOTARTIFACT1"
	inventory.pickupsound "weapons/unmaker/demonkey"
	+INVENTORY.ALWAYSPICKUP
	+FLOATBOB
	states
	{
	Spawn:
		ART1 ABCDE 3 bright
		loop
	Pickup:
		TNT1 A 0 A_GiveInventory("DemonKey", 1)
		stop
	}
}