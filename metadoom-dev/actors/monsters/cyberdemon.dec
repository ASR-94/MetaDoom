ACTOR MetaCyberdemon : MonsterCore
{
	Health 4000
	Radius 40
	Height 110
	Mass 1000
	Speed 16
	PainChance 20
	Monster
	MinMissileChance 160
	+BOSS 
	+MISSILEMORE
	+FLOORCLIP
	+NORADIUSDMG
	+DONTMORPH
	//+BOSSDEATH
	SeeSound "cyber/sight"
	PainSound "cyber/pain"
	DeathSound "cyber/death"
	ActiveSound "cyber/active"
	Obituary "$OB_CYBORG"
	damagefactor ChaingunTurret, 0.2
	painchance ChaingunTurret, 0
	States
	{
	Spawn:
		CYBR AB 10 A_Look
		Loop
	See:
		"####" A 0
		{
			if (CountInv("BleedingCounter") > 0)
			{
				A_DamageSelf ((5), "Bleeding");
				A_TakeInventory("BleedingCounter", 1);
				A_CustomMissile ("FlyingBlood", 0, 0, random (0, 360), 2, random (0, 160));
				A_PlaySound("monsters/bleed", CHAN_5);
			}
		}
		CYBR A 3 A_Hoof
		CYBR ABBCC 3 A_Chase
		CYBR D 3 A_Metal
		CYBR D 3 A_Chase
		Loop
	Missile:
		CYBR E 6 A_FaceTarget
		CYBR F 12 
		{
			A_CustomMissile("CyberdemonRocket", 48, -16);
			A_PlaySound ("weapons/rocketlauncher/fire", CHAN_WEAPON);
		}
		CYBR E 12 A_FaceTarget
		CYBR F 12 
		{
			A_CustomMissile("CyberdemonRocket", 48, -16);
			A_PlaySound ("weapons/rocketlauncher/fire", CHAN_WEAPON);
		}
		CYBR E 12 A_FaceTarget
		CYBR F 12 
		{
			A_CustomMissile("CyberdemonRocket", 48, -16);
			A_PlaySound ("weapons/rocketlauncher/fire", CHAN_WEAPON);
		}
		Goto See
	Pain.Axe:
	Pain.Fist:
	Pain.BerserkFist:
	Pain:
		CYBR G 10 A_Pain
		Goto See
	Death:
		CYBR H 10 A_GiveToTarget("ScoreItem", 10000)
		CYBR I 10 A_Scream
		CYBR JKL 10
		CYBR M 10 A_NoBlocking
		CYBR NO 10
		CYBR P 30
		CYBR P -1 A_KillMaster //A_BossDeath
		Stop
	}
}

ACTOR CyberdemonRocket
{
	Radius 11
	Height 8
	Speed 20
	Damage 20
	Projectile
	+RANDOMIZE
	Obituary "$OB_MPROCKET"
	Decal Scorch
	States
	{
	Spawn:
		MISL A 0 A_PlaySound ("weapons/rocketlauncher/loop", CHAN_BODY, 1.0, 1)
		MISL A 0 A_SpawnItem ("RocketTrailSmoke", 0, 6, 0, 0)
		MISL AAAA 1 Bright A_JumpIfInTargetInventory("RLDetonator",1,"Death")
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("Explosion",0,0,24,0,0,0,0,SXF_TRANSFERPOINTERS)
		Stop
	}
}