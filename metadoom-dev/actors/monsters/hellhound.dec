ACTOR MetaHellhound : MonsterCore
{
	Health 100
	Speed 15
	FastSpeed 20
	PainChance 180
	Radius 25
	Height 48
	Mass 100
	Monster
	ActiveSound "monsters/hellhound/bark"
	AttackSound "monsters/hellhound/bite"
	DeathSound "monsters/hellhound/die"
	PainSound "monsters/hellhound/pain"
	SeeSound "monsters/hellhound/see"
	Obituary "$OB_DOG1"
	painchance HolyWater, 255
	damagefactor DogCollar, 9999
	painchance DogCollar, 255
	scale 1.25
	States
	{
	Spawn:
		HDOG AB 10 A_Look
		Loop
	See:
		"####" A 0
		{
			if (CountInv("BleedingCounter") > 0)
			{
				A_DamageSelf ((5), "Bleeding");
				A_TakeInventory("BleedingCounter", 1);
				A_CustomMissile ("FlyingBlood", 0, 0, random (0, 360), 2, random (0, 160));
				A_PlaySound("monsters/bleed", CHAN_5);
			}
		}
		HDOG AABBCCDD 2 Fast A_Chase
		Loop
	Melee:
		HDOG EF 2 A_FaceTarget
		HDOG G 8 A_SargAttack
		HDOG FE 2
		Goto See
	Pain:
		HDOG H 0
		HDOG H 2
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		HDOG H 2 A_Pain
		Goto See
	Pain.HolyWater:
		HDOG H 2
		HDOG H 2 A_Pain
		Goto Scurred
	Punched:
		HDOG H 3 
		HDOG H 3
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		HDOG H 12 A_Pain
		Goto See
	Scurred:
		HDOG A 0 A_ChangeFlag("FRIGHTENED", TRUE)
	ScurredLoop:
		HDOG A 0 Fast A_GiveInventory("ScurredCounter", 1)
		HDOG A 2 Fast A_Chase("", "")
		HDOG A 2 Fast A_Chase("", "")
		HDOG A 0 Fast A_SpawnItem("HolySweat",0,54)
		HDOG B 2 Fast A_Chase("", "")
		HDOG B 2 Fast A_Chase("", "")
		HDOG C 2 Fast A_Chase("", "")
		HDOG C 2 Fast A_Chase("", "")
		HDOG C 0 Fast A_SpawnItem("HolySweat",0,54)
		HDOG D 2 Fast A_Chase("", "")
		HDOG D 2 Fast A_Chase("", "")
		HDOG D 0 Fast A_JumpIfInventory("ScurredCounter", 6, "ScurredEnd")
		loop
	ScurredEnd:
		HDOG D 0
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		goto See
	Death:
		HDOG H 0 A_Jump (128, 2)
		HDOG H 0 A_SetScale(-1.25,1.25) // random chance of sprite flipping
		HDOG H 8 A_GiveToTarget("ScoreItem", 250)
		HDOG I 8 A_Scream
		HDOG J 4 A_Fall
		HDOG K 4 A_PlaySound("bodyfalls/medium")
		HDOG K -1 
	XDeath:
		HDOG H 5 A_GiveToTarget("ScoreItem", 250)
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		Stop
	Death.DogCollar:
		HDOG A 0 A_PlaySound("inventory/dogcollar/activate")
		HDOG A 0 A_SpawnDebris("CollarSpark")
		HDOG A 0 A_GiveToTarget("MetaDogGun_HellHound", 1)
		HDOG A 0 A_GiveToTarget("MakeMeGrabTheDogGun_Hellhound", 1)
		stop
	Raise:
		HDOG K 0  A_SetScale(1.25,1.25)
		HDOG KJIH 5
		Goto See
	}
}

ACTOR MakeMeGrabTheDogGun_Hellhound : CustomInventory
{
  +COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  States
  {
  Spawn:
    TNT1 A 1 Bright
    Loop
  Pickup:
    TNT1 A 0 A_SelectWeapon("MetaDogGun_HellHound")
    Stop
  }
}