/*
----------------------------
MetaDoom
Actor Definitions
Heavy Weapons Dude
----------------------------
*/

// Uggh, these fuckers. This one is more or less the same as he always was, but
// reskinned to use an Assault Rifle instead of a Chaingun.

ACTOR MetaMachineGunGuy : MonsterCore
{
	Health 70
	Radius 20
	Height 56
	Mass 100
	Speed 8
	PainChance 170
	Monster
	+FLOORCLIP
	+DOHARMSPECIES
	SeeSound "monsters/machinegunner/see"
	PainSound "monsters/machinegunner/pain"
	DeathSound "monsters/machinegunner/die"
	ActiveSound "monsters/machinegunner/idle"
	AttackSound "chainguy/attack"
	Obituary "$OB_MGUNGUY"
	Dropitem "MetaMachineGun"
	damagefactor HolyWater, 0.25
	painchance HolyWater, 255
	damagefactor Axe, 50
	States
	{
	Spawn:
		MPOS AB 10 A_Look
		Loop
	See:
		"####" A 0 A_MetaBleed("FlyingBlood")
		MPOS AABBCCDD 3 A_Chase
		Loop
	Missile:
		MPOS E 10 A_FaceTarget
		MPOS FE 4 BRIGHT 
		{
			A_PlaySound ("weapons/machinegun/fire");
			A_CustomMissile("MonsterZombiemanTracer",32,0,random(-4,4),CMF_OFFSETPITCH,random(-4,4));
		}
		MPOS F 1 A_CPosRefire
		Goto Missile+1
	Pain:
		MPOS G 0
		MPOS G 3
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		MPOS G 3 A_Pain
		Goto See
	Pain.HolyWater:
		MPOS G 3
		MPOS G 3 A_Pain
		Goto Scurred
	Punched:
		MPOS G 3 
		MPOS G 3
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		MPOS G 12 A_Pain
		Goto See
	Scurred:
		MPOS A 0 A_ChangeFlag("FRIGHTENED", TRUE)
	ScurredLoop:
		MPOS A 0 A_GiveInventory("ScurredCounter", 1)
		MPOS A 2 A_Chase("", "")
		MPOS A 2 A_Chase("", "")
		MPOS A 0 A_SpawnItem("HolySweat",0,54)
		MPOS B 2 A_Chase("", "")
		MPOS B 2 A_Chase("", "")
		MPOS C 2 A_Chase("", "")
		MPOS C 2 A_Chase("", "")
		MPOS C 0 A_SpawnItem("HolySweat",0,54)
		MPOS D 2 A_Chase("", "")
		MPOS D 2 A_Chase("", "")
		MPOS D 0 A_JumpIfInventory("ScurredCounter", 6, "ScurredEnd")
		loop
	ScurredEnd:
		MPOS D 0
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		goto See
	Death:
		MPOS H 0 A_Jump (128, 2)
		MPOS H 0 A_SetScale(-1,1) // random chance of sprite flipping
		TNT1 A 0 A_SpawnItem("SoulSpawner")
		MPOS H 5 A_GiveToTarget("ScoreItem", 300)
		MPOS I 5 A_Scream
		MPOS J 5 A_NoBlocking
		MPOS KL 5
		MPOS M -1 A_PlaySound("bodyfalls/medium")
		Stop
	XDeath:
		TNT1 A 0 A_SpawnItem("SoulSpawner")
		CPOS O 5 A_GiveToTarget("ScoreItem", 300)
		"####" # 0 A_Fall
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		Stop
	Raise:
		MPOS M 5 A_SetScale(1,1)
		MPOS LKJIH 5
		Goto See
	}
}

// The upgraded version. Takes a little while to spin up, but fires at a far
// greater firing rate (and a much lower accuracy) to keep things interesting.

ACTOR MetaChainGunGuy : MonsterCore
{
	Health 80
	Radius 20
	Height 56
	Mass 100
	Speed 8
	PainChance 170
	Monster
	+FLOORCLIP
	+DOHARMSPECIES
	SeeSound "monsters/machinegunner/see"
	PainSound "monsters/machinegunner/pain"
	DeathSound "monsters/machinegunner/die"
	ActiveSound "monsters/machinegunner/idle"
	AttackSound "chainguy/attack"
	Obituary "$OB_CGUNGUY"
	Dropitem "MetaChainGun"
	damagefactor HolyWater, 0.25
	painchance HolyWater, 255
	damagefactor Axe, 50
	//translation "30:47=[88,140,237]:[10,26,46]"
	States
	{
	Spawn:
		CPOS AB 10 A_Look
		Loop
	See:
		"####" A 0 A_MetaBleed("FlyingBlood")
		CPOS AABBCCDD 3 A_Chase
		Loop
	Missile:
		CPOS E 0 A_PlaySound("weapons/chaingun/windup", CHAN_WEAPON)
		CPOS EEEEEEEEEEEEEEEEEEEEEEEEEEE 1 A_FaceTarget
	Brapp:
		CPOS FE 1 BRIGHT 
		{
			A_PlaySound ("weapons/chaingun/fire", CHAN_WEAPON);
			A_CustomMissile("MonsterCommandoTracer",32,0,random(-16,16),CMF_OFFSETPITCH,random(-4,4));
		}
		CPOS F 1 A_MonsterRefire (10, "WindDown")
		loop
	Winddown:
		CPOS E 15 A_PlaySound("weapons/chaingun/winddown", CHAN_WEAPON)
		goto See
	Pain:
		CPOS G 0
		CPOS G 0 A_StopSound(CHAN_WEAPON)
		CPOS G 3
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		CPOS G 3 A_Pain
		Goto See
	Pain.HolyWater:
		CPOS G 0
		CPOS G 0 A_StopSound(CHAN_WEAPON)
		CPOS G 3
		CPOS G 3 A_Pain
		Goto Scurred
	Punched:
		CPOS G 3 
		CPOS G 3
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		CPOS G 12 A_Pain
		Goto See
	Scurred:
		CPOS A 0 A_ChangeFlag("FRIGHTENED", TRUE)
	ScurredLoop:
		CPOS A 0 A_GiveInventory("ScurredCounter", 1)
		CPOS A 2 A_Chase("", "")
		CPOS A 2 A_Chase("", "")
		CPOS A 0 A_SpawnItem("HolySweat",0,54)
		CPOS B 2 A_Chase("", "")
		CPOS B 2 A_Chase("", "")
		CPOS C 2 A_Chase("", "")
		CPOS C 2 A_Chase("", "")
		CPOS C 0 A_SpawnItem("HolySweat",0,54)
		CPOS D 2 A_Chase("", "")
		CPOS D 2 A_Chase("", "")
		CPOS D 0 A_JumpIfInventory("ScurredCounter", 6, "ScurredEnd")
		loop
	ScurredEnd:
		CPOS D 0
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		goto See
	Death:
		CPOS H 0 A_Jump (128, 2)
		CPOS H 0 A_SetScale(-1,1) // random chance of sprite flipping
		TNT1 A 0 A_SpawnItem("SoulSpawner")
		CPOS G 0 A_StopSound(CHAN_WEAPON)
		CPOS H 5 A_GiveToTarget("ScoreItem", 300)
		CPOS I 5 A_Scream
		CPOS J 5 A_NoBlocking
		CPOS KLM 5
		CPOS N -1 A_PlaySound("bodyfalls/medium")
		Stop
	XDeath:
		TNT1 A 0 A_SpawnItem("SoulSpawner")
		CPOS G 0
		CPOS G 0 A_StopSound(CHAN_WEAPON)
		CPOS O 5 A_GiveToTarget("ScoreItem", 300)
		"####" # 0 A_Fall
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		Stop
	Raise:
		CPOS N 5 A_SetScale(1,1)
		CPOS MLKJIH 5
		Goto See
	}
}

// Former Assassin, aka The Doom 3 Chaingunner. Smarter, and has a tentacle pull attack.

ACTOR MetaAssassinGuy : MonsterCore
{
	Health 80
	Radius 20
	Height 56
	Mass 100
	Speed 8
	PainChance 170
	Monster
	+FLOORCLIP
	+DOHARMSPECIES
	SeeSound "monsters/d3chaingunner/see"
	PainSound "monsters/d3chaingunner/pain"
	DeathSound "monsters/d3chaingunner/die"
	ActiveSound "monsters/d3chaingunner/idle"
	AttackSound "chainguy/attack"
	Obituary "$OB_D3CGUNGUY"
	Dropitem "MetaChainGun"
	damagefactor HolyWater, 0.25
	painchance HolyWater, 255
	damagefactor Axe, 50
	States
	{
	Spawn:
		PSCM AB 10 A_Look
		Loop
	See:
		"####" A 0 A_MetaBleed("FlyingBlood")
		PSCM AABBCCDD 3 A_Chase
		Loop
	Missile:
		PSCM E 0 A_Jump(64, "MissileTentacle")
		PSCM E 0 A_PlaySound("weapons/chaingun/windup", CHAN_WEAPON)
		PSCM EEEEEEEEEEEEEE 1 A_FaceTarget
	Brapp:
		PSCM FE 1 BRIGHT 
		{
			A_PlaySound ("weapons/chaingun/fire", CHAN_WEAPON);
			A_CustomMissile("MonsterCommandoTracer",32,0,random(-16,16),CMF_OFFSETPITCH,random(-4,4));
		}
		PSCM F 1 A_MonsterRefire (80, "WindDown")
		loop
	Winddown:
		PSCM E 10 A_PlaySound("weapons/chaingun/winddown", CHAN_WEAPON)
		goto See
	MissileTentacle:
		PSCM A 5 A_FaceTarget
		PSCM E 5 A_FaceTarget
		PSCM G 20 BRIGHT A_CustomMissile ("AssassinTentacle", 32, 0, 0)
		PSCM EA 5 A_FaceTarget
		Goto See
	Pain:
		PSCM H 0
		PSCM H 0 A_StopSound(CHAN_WEAPON)
		PSCM H 3
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		PSCM H 3 A_Pain
		Goto See
	Pain.HolyWater:
		PSCM H 0
		PSCM H 0 A_StopSound(CHAN_WEAPON)
		PSCM H 3
		PSCM H 3 A_Pain
		Goto Scurred
	Punched:
		PSCM H 3 
		PSCM H 3
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		PSCM H 12 A_Pain
		Goto See
	Scurred:
		PSCM A 0 A_ChangeFlag("FRIGHTENED", TRUE)
	ScurredLoop:
		PSCM A 0 A_GiveInventory("ScurredCounter", 1)
		PSCM A 2 A_Chase("", "")
		PSCM A 2 A_Chase("", "")
		PSCM A 0 A_SpawnItem("HolySweat",0,54)
		PSCM B 2 A_Chase("", "")
		PSCM B 2 A_Chase("", "")
		PSCM C 2 A_Chase("", "")
		PSCM C 2 A_Chase("", "")
		PSCM C 0 A_SpawnItem("HolySweat",0,54)
		PSCM D 2 A_Chase("", "")
		PSCM D 2 A_Chase("", "")
		PSCM D 0 A_JumpIfInventory("ScurredCounter", 6, "ScurredEnd")
		loop
	ScurredEnd:
		PSCM D 0
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		goto See
	Death:
		PSCM H 0 A_Jump (128, 2)
		PSCM H 0 A_SetScale(-1,1) // random chance of sprite flipping
		TNT1 A 0 A_SpawnItem("SoulSpawner")
		PSCM G 0 A_StopSound(CHAN_WEAPON)
		PSCM I 5 A_GiveToTarget("ScoreItem", 300)
		PSCM J 5 A_Scream
		PSCM K 5 A_NoBlocking
		PSCM L 5 A_PlaySound("bodyfalls/medium")
		PSCM M 5 
		PSCM N 5
		PSCM O -1 
		Stop
	XDeath:
		TNT1 A 0 A_SpawnItem("SoulSpawner")
		PSCM G 0
		PSCM G 0 A_StopSound(CHAN_WEAPON)
		PSCM P 5 A_GiveToTarget("ScoreItem", 300)
		"####" # 0 A_Fall
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		Stop
	Raise:
		PSCM O 5 A_SetScale(1,1)
		PSCM NMLKJI 5
		Goto See
	}
}

ACTOR AssassinTentacle : LoreShot
{
	Speed 15
	Height 14
	Radius 10
	Projectile
	//+STRIFEDAMAGE
	Damage 2
	MaxStepHeight 4
	SeeSound "monsters/d3chaingunner/tentacle"
	States
	{
	Spawn:
		CTNG AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItem("AssassinTentacleTrail")
		stop
	Death:
		CTNG A 0 A_PlaySound("monsters/wretched/pull")
		Stop
	}
}

ACTOR AssassinTentacleTrail
{
	+NOBLOCKMAP
	+NOGRAVITY
	scale 0.8
	states
	{
	Spawn:
		CTNG A 0
		CTNG A 32 
		stop
	}
}