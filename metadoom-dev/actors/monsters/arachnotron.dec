/*
----------------------------
MetaDoom
Actor Definitions
Arachnotron
----------------------------
*/

// Currently more or less identical to the original Doom monster, behavior-wise.
// TODO: Twiddle with, add at least one variant!

ACTOR MetaArachnotron : MonsterCore
{
	Health 500
	Radius 64
	Height 64
	Mass 600
	Speed 12
	PainChance 128
	Monster
	+FLOORCLIP
	//+BOSSDEATH
	SeeSound "monsters/arachnotron/see"
	PainSound "monsters/arachnotron/pain"
	DeathSound "monsters/arachnotron/die1"
	ActiveSound "monsters/arachnotron/idle"
	Obituary "$OB_BABY"
	States
	{
	Spawn:
		BSPI AB 10 A_Look
		Loop
	See:
		"####" A 0 A_MetaBleed("FlyingBlood")
		BSPI A 20
		BSPI A 3 { A_Chase; A_PlaySound("monsters/arachnotron/walk"); }
		BSPI ABBCC 3 A_Chase
		BSPI D 3 { A_Chase; A_PlaySound("monsters/arachnotron/walk"); }
		BSPI DEEFF 3 A_Chase
		Goto See+2
	Missile:
		BSPI A 20 BRIGHT A_FaceTarget
		BSPI G 4 BRIGHT A_CustomMissile("MetaArachnotronPlasma")
		BSPI H 4 BRIGHT
		BSPI H 1 BRIGHT A_SpidRefire
		Goto Missile+1
	Pain:
		BSPI I 3
		BSPI I 3 A_Pain
		Goto See+2
	Death:
		BSPI J 0 A_Jump (128, 2)
		BSPI J 0 A_SetScale(-1,1) // random chance of sprite flipping
		BSPI J 0 A_GiveToTarget("ScoreItem", 600)
		TNT1 A 0 A_SpawnItem("SoulSpawner")
		BSPI J 0 A_PlaySound("monsters/arachnotron/die2")
		BSPI J 20 A_Scream
		BSPI K 7 A_NoBlocking
		BSPI L 7
		BSPI M 7 A_PlaySound("monsters/arachnotron/body")
		BSPI NO 7
		BSPI P -1 A_KillMaster //A_BossDeath
		Stop
    Raise:
		BSPI P 0 A_SetScale(1,1)
		BSPI P 5
		BSPI ONMLKJ 5
		Goto See+2
	}
}

ACTOR MetaArachnotronPlasma
{
	Radius 13
	Height 8
	Speed 25
	Damage 5
	Projectile 
	+RANDOMIZE
	RenderStyle Add 
	Alpha 0.75
	decal ArachnotronScorch
	SeeSound "monsters/arachnotron/fire"
	DeathSound "weapons/plasmarifle/hit"
	States
	{
	Spawn:
		APLS AB 5 BRIGHT
		Loop
	Death:
		APBX ABCDE 5 BRIGHT
		Stop
	}
}


