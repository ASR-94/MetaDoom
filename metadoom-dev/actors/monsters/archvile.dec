/*
----------------------------
MetaDoom
Actor Definitions
Archvile
----------------------------
*/

// Currently more or less identical to the original Doom monster, behavior-wise.
// TODO: Twiddle with, add at least one variant!

ACTOR MetaArchvile : MonsterCore
{
	Health 700
	Radius 20
	Height 56
	Mass 500
	Speed 15
	PainChance 10
	Monster
	MaxTargetRange 896
	DropItem "BFGAmmo"
	+QUICKTORETALIATE 
	+FLOORCLIP 
	+NOTARGET
	SeeSound "vile/sight"
	PainSound "vile/pain"
	DeathSound "vile/death"
	ActiveSound "vile/active"
	MeleeSound "vile/stop"
	Obituary "$OB_VILE"
	damagefactor HolyWater, 1.5
	painchance HolyWater, 255
	States
	{
	Spawn:
		VILE AB 10 A_Look
		Loop
	See:
		"####" A 0 A_MetaBleed("FlyingBlood")
		VILE AABBCCDDEEFF 2 A_VileChase
		Loop
	Missile:
		VILE G 0 BRIGHT A_PlaySound("monsters/archvile/magic", CHAN_VOICE)
		VILE G 10 BRIGHT A_FaceTarget 
		VILE H 8 BRIGHT A_VileTarget("MetaArchvileFire")
		VILE IJKLMN 8 BRIGHT A_FaceTarget
		VILE O 0 BRIGHT A_JumpIfInTargetInventory("FireExtinguished",1,"MissileFailed")
		VILE O 8 BRIGHT A_VileAttack("monsters/archvile/boom", 20, 70, 70, 1, "Fire")
		VILE P 20 BRIGHT
		Goto See
	MissileFailed:
		VILE O 8 BRIGHT A_PlaySound("monsters/archvile/dud")
		VILE P 20 BRIGHT
		VILE A 10
		VILE Q 10
		VILE A 5
		Goto See
	Heal:
		VILE A 0
		VILE A 0 A_PlaySound("monsters/archvile/resurrect", CHAN_VOICE)
		VILE "[\]" 10 BRIGHT
		Goto See
	Pain:
		VILE Q 0
		VILE Q 5 
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		VILE Q 5 A_Pain
		Goto See
	Pain.HolyWater:
		VILE Q 5 
		VILE Q 5 A_Pain
		Goto Scurred
	Scurred:
		VILE A 0 A_ChangeFlag("FRIGHTENED", TRUE)
	ScurredLoop:
		VILE A 0 A_GiveInventory("ScurredCounter", 1)
		VILE A 2 A_Chase("", "")
		VILE A 2 A_Chase("", "")
		VILE A 0 A_SpawnItem("HolySweat",0,54)
		VILE B 2 A_Chase("", "")
		VILE B 2 A_Chase("", "")
		VILE C 2 A_Chase("", "")
		VILE C 2 A_Chase("", "")
		VILE C 0 A_SpawnItem("HolySweat",0,54)
		VILE D 2 A_Chase("", "")
		VILE D 2 A_Chase("", "")
		VILE E 2 A_Chase("", "")
		VILE E 2 A_Chase("", "")
		VILE E 0 A_SpawnItem("HolySweat",0,54)
		VILE F 2 A_Chase("", "")
		VILE F 2 A_Chase("", "")
		VILE F 0 A_JumpIfInventory("ScurredCounter", 6, "ScurredEnd")
		loop
	ScurredEnd:
		VILE F 0
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		goto See
	Death:
		VILE Q 7 			// Make a filthy great mess once the Archvile dies,
		{					// to make a point of it not being rez-ible.
			A_GiveToTarget("ScoreItem", 1000);
			A_PlaySound("monsters/archvile/gib",CHAN_AUTO,0.8);
			A_CustomMissile ("FlyingBlood", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("FlyingBlood", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("FlyingBlood", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("FlyingBlood", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("FlyingBlood", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("MetaSploosh", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("MetaSploosh", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("MetaSploosh", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("MetaSploosh", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("MetaSploosh", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_SpawnItemEx ("MetaGib3", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib4", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib5", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib6", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib3", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib4", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib5", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib6", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItem("BloodSplat");
			A_SpawnItem("SoulSpawner");
		}
		VILE R 7 A_Scream
		VILE S 7 A_NoBlocking
		VILE TUVWXY 7
		VILE Z -1
		Stop
	}
}


//===========================================================================
//
// Arch Vile Fire
//
//===========================================================================

ACTOR MetaArchvileFire
{
	+NOBLOCKMAP +NOGRAVITY
	RenderStyle Add
	Alpha 1
	States
	{
	Spawn:
		FIRE A 2 BRIGHT  A_StartFire
		FIRE BAB 2 BRIGHT  A_Fire
		FIRE C 2 BRIGHT  { A_PlaySound("monsters/archvile/flames"); A_Fire; }
		FIRE BCBCDCDCDEDED 2 BRIGHT  A_Fire
		FIRE E 2 BRIGHT  { A_PlaySound("monsters/archvile/flames"); A_Fire; }
		FIRE FEFEFGHGHGH 2 BRIGHT  A_Fire
		Stop
	}
}

// Hurls popcorn enemies out in babby bosscubes and zooms around like a bastard

ACTOR MetaSummoner : MonsterCore
{
	Health 600
	Radius 20
	Height 56
	Mass 500
	Speed 16
	PainChance 10
	Monster
	MaxTargetRange 896
	DropItem "BFGAmmo"
	+QUICKTORETALIATE 
	+FLOORCLIP 
	+NOTARGET
	+FLOATBOB
	SeeSound "monsters/summoner/see"
	PainSound "monsters/summoner/pain"
	DeathSound "monsters/summoner/die"
	ActiveSound "monsters/summoner/idle"
	MeleeSound "vile/stop"
	Obituary "$OB_SUMMONER"
	damagefactor HolyWater, 1.5
	painchance HolyWater, 255
	yscale 0.9
	States
	{
	Spawn:
		SUMN ABCDCB 10 A_Look
		Loop
	See:
		"####" A 0 A_MetaBleed("FlyingBlood")
		SUMN E 0 A_Jump(90, "Teleport")
		SUMN EEFFGG 2 A_VileChase
		SUMN G 0 A_Jump(90, "Teleport")
		SUMN GGFF 2 A_VileChase
		VILE A 0 A_Jump(90, "Popcorn")
		Loop
	SeeAlreadyTeleportedLately:
		"####" A 0 A_MetaBleed("FlyingBlood")
		SUMN EEFFGG 2 A_VileChase
		SUMN GGFF 2 A_VileChase
		SUMN A 0 A_Jump(90, "Popcorn")
		goto See
	Missile:
		SUMN A 0 A_Jump(90, "Popcorn")
		SUMN I 0 BRIGHT A_PlaySound("monsters/archvile/magic", CHAN_VOICE)
		SUMN I 10 BRIGHT A_FaceTarget 
		SUMN J 8 BRIGHT A_VileTarget("MetaArchvileFire")
		SUMN KLMN 8 BRIGHT A_FaceTarget
		SUMN O 0 BRIGHT A_JumpIfInTargetInventory("FireExtinguished",1,"MissileFailed")
		SUMN O 8 BRIGHT A_VileAttack("monsters/archvile/boom", 20, 70, 70, 1, "Fire")
		SUMN PQ 10 BRIGHT
		Goto See
	MissileFailed:
		SUMN O 8 BRIGHT A_PlaySound("monsters/archvile/dud")
		SUMN P 20 BRIGHT
		SUMN A 10
		SUMN S 10
		SUMN A 5
		Goto See
	Heal:
		SUMN A 0
		SUMN A 0 A_PlaySound("monsters/archvile/resurrect", CHAN_VOICE)
		SUMN R 10 BRIGHT
		Goto See
	Popcorn:
		SUMN A 0
		SUMN NLKJ 4
		SUMN I 0 A_JumpIf((ceilingz - floorz) < 256, "PopcornLow")
	PopcornHigh:
		SUMN N 20 
		{
			A_PlaySound("monsters/summoner/popcorn", CHAN_VOICE);
			A_SpawnItemEx("MetaSummonerPopcorn",0,0,54,random(8,16),0,random(0,8),random(0,360));
			A_SpawnItemEx("MetaSummonerPopcorn",0,0,54,random(8,16),0,random(0,8),random(0,360));
			A_SpawnItemEx("MetaSummonerPopcorn",0,0,54,random(8,16),0,random(0,8),random(0,360));
			A_SpawnItemEx("MetaSummonerPopcorn",0,0,54,random(8,16),0,random(0,8),random(0,360));
		}
		SUMN RA 4
		goto See
	PopcornLow:
		SUMN N 20 
		{
			A_PlaySound("monsters/summoner/popcorn", CHAN_VOICE);
			A_SpawnItemEx("MetaSummonerPopcorn",0,0,32,random(8,16),0,random(0,8),random(0,360));
			A_SpawnItemEx("MetaSummonerPopcorn",0,0,32,random(8,16),0,random(0,8),random(0,360));
			A_SpawnItemEx("MetaSummonerPopcorn",0,0,32,random(8,16),0,random(0,8),random(0,360));
			A_SpawnItemEx("MetaSummonerPopcorn",0,0,32,random(8,16),0,random(0,8),random(0,360));
		}
		SUMN RA 4
		goto See
	Teleport:
		SUMN A 0
		SUMN A 5 BRIGHT
		SUMN B 4 BRIGHT
		SUMN C 2 BRIGHT
		SUMN DABD 1 BRIGHT
		VILE A 0 A_PlaySound("monsters/summoner/warp", CHAN_6)
		Goto TeleLoop
	TeleLoop:
		TNT1 AAAAA 0 A_Chase("", "")
		TNT1 A 0 A_SpawnItem("MetaSummonerWarpTrail")
		TNT1 AAAAA 0 A_Chase("", "")
		TNT1 A 0 A_SpawnItem("MetaSummonerWarpTrail")
		TNT1 AAAAA 0 A_Wander
		TNT1 A 0 A_SpawnItem("MetaSummonerWarpTrail")
		TNT1 AAAAA 0 A_Wander
		TNT1 A 0 A_Jump(128, "TeleStop")
		loop
	TeleStop:
		VILE A 0
		VILE A 0 A_PlaySound("monsters/summoner/warp", CHAN_7)
		SUMN ABC 4 BRIGHT
		Goto SeeAlreadyTeleportedLately
	Pain:
		SUMN R 0
		SUMN R 5 
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		SUMN R 5 A_Pain
		SUMN A 0 A_Jump(128, "Teleport")
		Goto See
	Pain.HolyWater:
		SUMN R 5 
		SUMN R 5 A_Pain
		Goto Scurred
	Scurred:
		VILE A 0 A_ChangeFlag("FRIGHTENED", TRUE)
	ScurredLoop:
		SUMN E 0 A_GiveInventory("ScurredCounter", 1)
		SUMN E 2 A_Chase("", "")
		SUMN E 2 A_Chase("", "")
		SUMN E 0 A_SpawnItem("HolySweat",0,54)
		SUMN F 2 A_Chase("", "")
		SUMN F 2 A_Chase("", "")
		SUMN G 2 A_Chase("", "")
		SUMN G 2 A_Chase("", "")
		SUMN G 0 A_SpawnItem("HolySweat",0,54)
		SUMN H 2 A_Chase("", "")
		SUMN H 2 A_Chase("", "")
		SUMN G 2 A_Chase("", "")
		SUMN G 2 A_Chase("", "")
		SUMN F 0 A_SpawnItem("HolySweat",0,54)
		SUMN F 2 A_Chase("", "")
		SUMN F 0 A_JumpIfInventory("ScurredCounter", 6, "ScurredEnd")
		loop
	ScurredEnd:
		SUMN F 0
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		goto See
	Death:
		SUMN T 7 			// Make a filthy great mess once the Archvile dies,
		{					// to make a point of it not being rez-ible.
			A_GiveToTarget("ScoreItem", 1000);
			A_PlaySound("monsters/archvile/gib",CHAN_AUTO,0.8);
			A_CustomMissile ("FlyingBlood", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("FlyingBlood", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("FlyingBlood", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("FlyingBlood", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("FlyingBlood", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("MetaSploosh", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("MetaSploosh", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("MetaSploosh", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("MetaSploosh", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("MetaSploosh", random(8,32), 0, random (0, 360), 2, random (0, 160));
			A_SpawnItemEx ("MetaGib3", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib4", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib5", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib6", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib3", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib4", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib5", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItemEx ("MetaGib6", 1, 1, random(8,32), random(0,5), random(0,5), random(8,18), random(0,360), 128, 0);
			A_SpawnItem("BloodSplat");
			A_SpawnItem("SoulSpawner");
			A_ChangeFlag("FLOATBOB", FALSE);
		}
		SUMN U 7 A_Scream
		SUMN V 7 A_NoBlocking
		SUMN WX 7
		SUMN Y -1
		Stop
	}
}

actor MetaSummonerPopcorn
{
	+CANBOUNCEWATER
	Radius 20
	Height 56
	Speed 20
	Damage 0
	PROJECTILE
	+RANDOMIZE
	-NOGRAVITY
	gravity 0.5
	scale 0.5
	SeeSound "inventory/throw"
	States
	{
	Spawn:
		BOSF A 0
		BOSF A 0 A_JumpIf((ceilingz - floorz) < 72, "SpawnLow")
	SpawnHigh:
		BOSF A 0 ThrustThingZ(0,32,0,1)
		goto Bounce
	SpawnLow:
		BOSF A 0 A_ScaleVelocity(1.5)
		goto Bounce
	Bounce:
		BOSF ABCD 3 BRIGHT
		loop
	Death:
		TNT1 A 0 A_PlaySound("inventory/siphongrenade/explode")
		TNT1 A 0 A_SpawnItem("MetaArchvileFire")
		TNT1 A 0 A_SpawnItem("MetaUnwilling")
		Stop
	}
}

ACTOR MetaSummonerWarpTrail
{
	radius 10
	height 10
	+NOCLIP
	+NOGRAVITY
	+NOBLOCKMAP
	alpha 0.5
	Renderstyle add
	states
	{
		Spawn:
		SUMN E 0
	DoTheThing:
		SUMN E 1
		{
			A_FadeOut(0.05);
		}
		loop
	}
}



ACTOR MetaUnwilling : MonsterCore
{
	Health 20
	Radius 20
	Height 56
	Mass 100
	Speed 4
	PainChance 200
	Monster
	+FLOORCLIP
	-COUNTKILL
	SeeSound "monsters/unwilling/see"
	PainSound "monsters/unwilling/pain"
	DeathSound "monsters/unwilling/die"
	ActiveSound "monsters/unwilling/idle"
	HitObituary "$OB_SUMMONER_POPCORN"
	Obituary "$OB_SUMMONER_POPCORN"
	damagefactor HolyWater, 0.25
	painchance HolyWater, 255
	damagefactor Axe, 50
	States
	{
	Spawn:
		UNWI AB 10 A_Look
		Loop
	See:
		"####" A 0 A_MetaBleed("FlyingBlood")
		UNWI AABBCCDD 3 A_Chase
		Loop
	Melee:
	Missile:
		UNWI E 0 A_PlaySound("monsters/unwilling/attack", CHAN_VOICE)
		UNWI EF 4 A_FaceTarget
		UNWI G 6 A_CustomComboAttack("UnwillingGibBall", 32, 3 * random(1, 8), "monsters/imp/claw")
		Goto See
	Pain:
		UNWI H 0
		UNWI H 2
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		UNWI H 2 A_Pain
		Goto See
	Pain.HolyWater:
		UNWI G 3
		UNWI G 3 A_Pain
		Goto Scurred
	Punched:
		UNWI G 3 
		UNWI G 3
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		UNWI G 12 A_Pain
		Goto See
	Scurred:
		UNWI A 0 A_ChangeFlag("FRIGHTENED", TRUE)
	ScurredLoop:
		UNWI A 0 A_GiveInventory("ScurredCounter", 1)
		UNWI A 2 A_Chase("", "")
		UNWI A 2 A_Chase("", "")
		UNWI A 0 A_SpawnItem("HolySweat",0,54)
		UNWI B 2 A_Chase("", "")
		UNWI B 2 A_Chase("", "")
		UNWI C 2 A_Chase("", "")
		UNWI C 2 A_Chase("", "")
		UNWI C 0 A_SpawnItem("HolySweat",0,54)
		UNWI D 2 A_Chase("", "")
		UNWI D 2 A_Chase("", "")
		UNWI D 0 A_JumpIfInventory("ScurredCounter", 6, "ScurredEnd")
		loop
	ScurredEnd:
		UNWI D 0
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		goto See
	Death:
		UNWI I 0 A_Jump (128, 2)
		UNWI I 0 A_SetScale(-1,1) // random chance of sprite flipping
		UNWI I 6 A_GiveToTarget("ScoreItem", 50)
		UNWI J 6 A_Scream
		UNWI K 5
		UNWI L 5 A_NoBlocking
		UNWI M -1 A_PlaySound("bodyfalls/medium")
		Stop
	XDeath:
		UNWI J 5 A_GiveToTarget("ScoreItem", 50)
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		Stop
	Raise:
		UNWI M 0 A_SetScale(1,1)
		UNWI MLKJI 8
		Goto See
	}
}

ACTOR UnwillingGibBall
{
	Radius 4
	Height 9
	Health 40
	Damage (random(1,4))
	Speed 35
	Gravity 0.5
	Mass 0
	PROJECTILE
	+CANBOUNCEWATER
	+NOGRAVITY
	ReactionTime 120
	Seesound "monsters/unwilling/fire"
	+NOBLOCKMAP 
	+MISSILE 
	decal BloodSplat1
	States
	{
	Spawn:
		ZGIB AAAAA 1 A_SpawnItemEx("UnwillingGibTrail",0,0,0)
		ZGIB A 0 A_ChangeFlag(NOGRAVITY, 0)
		Loop
	Death:
		TNT1 A 1 A_PlaySound ("monsters/unwilling/gib")
		TNT1 A 0
		{
			if (z <= (floorz + 32))
			{
				A_SpawnItem("BloodSplatSmall");
			}
		}
		Stop
	Crash:
		TNT1 A 1 A_PlaySound ("monsters/unwilling/gib")
		Stop
	}
}

Actor UnwillingGibTrail
{
	Health 3
	+NOBLOCKMAP
	+NOTELEPORT
	+NOGRAVITY
	Scale 0.8
	RenderStyle Translucent
	Alpha 0.8
	States
	{
	Spawn:
		ZBLD ABCD 3
		Stop
	}
}