/*
----------------------------
MetaDoom
Actor Definitions
Optional Content
----------------------------
*/

// Scripted Marines, often seen in old ZDoom mapsets standing in for any given NPC.
// These are set up to use a VWEP system to give them a little more variety.
// To be honest, I'd love to give them helmets from Doom 2016 multiplayer,
// but that's a lot of sprite to alter by this point...

class MetaScriptedMarine : MonsterCore // replaces ScriptedMarine
{
	Default
	{
		Health 100;
		Radius 16;
		Height 56;
		Mass 100;
		Speed 8;
		Painchance 160;
		MONSTER;
		-COUNTKILL
		Translation 0;
		Damage 100;
		DeathSound "*death";
		PainSound "*pain50";
		Obituary "$OB_MARINE";
	}

	States
	{
	Spawn:
		OPLA A 4 A_Look;
		Loop;
	See:
		"####" A 0 A_MetaBleed("FlyingBlood");
		"####" ABCD 4 A_Chase;
		Loop;
	Missile:
		"####" E 12 A_FaceTarget;
		Goto See;
	SkipAttack:
		"####" A 1;
		Goto See;
	Pain:
		"####" G 4;
		"####" G 4 A_Pain;
		Goto See;
	Death:
		"####" H 0 { bSPRITEFLIP = random(0,1); } // random chance of sprite flipping
		"####" H 0 A_SpawnItem("SoulSpawner");
		"####" H 10 A_StopSound(CHAN_BODY);
		"####" I 10 A_Scream;
		"####" J 10 A_NoBlocking;
		"####" K 10;
		"####" M 10 A_PlaySound("bodyfalls/medium");
		"####" M 10;
		"####" N -1;
		Stop;
	XDeath:
		"####" O 0 A_SpawnItem("SoulSpawner");
		"####" O 5 A_StopSound(CHAN_BODY);
		"####" "#" 0 A_Fall;
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		"####" M 0 { bSPRITEFLIP = 0; }
		"####" MLKJIH 5;
		Goto See;
	}
}

// Fist Marine. Largely irrelevant and doomed.

class MetaMarineFist : MetaScriptedMarine // replaces MarineFist
{
	Default
	{
		Obituary "$OB_MARINE_FIST";
		DropItem "ArmorBonus";
	}

	States
	{
	Spawn:
		PLAF A 4 A_Look;
		Loop;
	Melee:
		PLAF A 4 A_FaceTarget;
		PLAF E 8 A_CustomMeleeAttack((2 * random(1, 10)), "weapons/melee/hitwall", "weapons/melee/wiff", "Default");
		PLAF A 4 A_FaceTarget;
		Goto See;
	Missile:
		Stop;
	}
}


// Berserk Marine, using the SCREAM LOOP from Doom 3 that I'd love to use elsewhere.

class MetaMarineBerserk : MetaMarineFist // replaces MarineBerserk
{
	Default
	{
		Obituary "$OB_MARINE_BERSERK";
		DropItem "ArmorBonus";
	}

	States
	{
	Spawn:
		PLAF A 0;
		PLAF A 0 A_PlaySound("player/berserk", CHAN_BODY, 1.0, 1);
		PLAF A 4 A_Look;
		Loop;
	Melee:
		PLAF A 4 A_FaceTarget;
		PLAF E 8 A_CustomMeleeAttack((20 * random(1, 10)), "weapons/melee/hitwall", "weapons/melee/wiff", "Default");
		PLAF A 4 A_FaceTarget;
		Goto See;
	}
}
// Chainsaw Marine. Psycho killer, qu'est-ce que c'est?

class MetaMarineChainsaw : MetaScriptedMarine // replaces MarineChainsaw
{
	Default
	{
		Obituary "$OB_MARINE_SAW";
		DropItem "ArmorBonus", 255;
		DropItem "MetaChainsaw", 32;
	}

	States
	{
	Spawn:
		PLCS A 0;
		PLCS A 0 A_PlaySound("weapons/chainsaw/idle", CHAN_BODY, 1.0, 1);
		PLCS A 4 A_Look;
		Loop;
	Melee:
		PLCS E 4 A_M_Saw ("weapons/chainsaw/slash", "weapons/chainsaw/slashhit");
		PLCS F 4 A_FaceTarget;
		PLCS E 0 A_CPosRefire;
		Goto See;
	Missile:
		Stop;
	}
}



// Pistol Marine. In the interests of fairness, its EMG sidearm is nerfed to the
// damage values of the original Doom pistol.

class MetaMarinePistol : MetaScriptedMarine // replaces MarinePistol
{
	Default
	{
		Obituary "$OB_MARINE_PISTOL";
		DropItem "Clip";
	}

	States
	{
	Spawn:
		PLAP A 4 A_Look;
		Loop;
	Missile:
		PLAP E 10 A_FaceTarget;
		PLAP E 0 A_PlaySound ("grunt/attack");
		PLAP F 8 
		{
			A_SpawnProjectile("MetaMarinePistolTracer");
			A_PlaySound("weapons/pistol/fire", CHAN_WEAPON);
		}
		PLAP E 8;
		Goto See;
	}
}

class MetaMarinePistolTracer : PistolTracer
{
	Default
	{
		DamageFunction (random(5,15));
	}
}

// Shotgun Marine. Basically a Shotgun Guy with more health and less flavor.

class MetaMarineShotgun : MetaScriptedMarine // replaces MarineShotgun
{
	Default
	{
		Obituary "$OB_MARINE_SHOTGUN";
		DropItem "Shell", 255;
		DropItem "MetaShotgun", 32;
	}

	States
	{
	Spawn:
		PLSG A 4 A_Look;
		Loop;
	Missile:
		PLSG E 10 A_FaceTarget;
		PLSG F 10 BRIGHT
		{
			A_SpawnProjectile("MonsterShotgunGuyTracer",32,0,random(-16,16),CMF_OFFSETPITCH,random(-4,4));
			A_SpawnProjectile("MonsterShotgunGuyTracer",32,0,random(-16,16),CMF_OFFSETPITCH,random(-4,16));
			A_SpawnProjectile("MonsterShotgunGuyTracer",32,0,random(-16,16),CMF_OFFSETPITCH,random(-4,4));
			A_SpawnProjectile("MonsterShotgunGuyTracer");
			A_PlaySound("weapons/shotgun/fire");
		}
		PLSG E 4;
		PLSG A 6 A_PlaySound("weapons/shotgun/cock1");
		PLSG E 6 A_PlaySound("weapons/shotgun/cock2");
		Goto See;
	}
}



// SSG Marine. Who likes insta-kills?

class MetaMarineSSG : MetaScriptedMarine // replaces MarineSSG
{
	Default
	{
		Obituary "$OB_MARINE_SSG";
		DropItem "Shell", 255;
		DropItem "MetaSuperShotgun", 32;
	}

	States
	{
	Spawn:
		PLS2 A 4 A_Look;
		Loop;
	Missile:
		PLS2 E 10 A_FaceTarget;
		PLS2 F 10 BRIGHT
		{
			for (int i = 0; i < 9; i++)
			{
				A_SpawnProjectile("MonsterShotgunGuyTracer",32,0,random(-24,24),CMF_OFFSETPITCH,random(-16,16));
			}

			A_SpawnProjectile("MonsterShotgunGuyTracer");
			A_PlaySound("weapons/ssg/fire");
		}
		PLS2 E 8;
		PLS2 A 10 A_PlaySound("weapons/ssg/open");
		PLS2 E 10 A_PlaySound("weapons/ssg/load");
		PLS2 A 10 A_PlaySound("weapons/ssg/close");
		Goto See;
	}
}

// Machine Gun marine. A proper chaingun marine wouldn't be fair.

class MetaMarineChaingun : MetaScriptedMarine // replaces MarineChaingun
{
	Default
	{
		Obituary "$OB_MARINE_MACHINEGUN";
		DropItem "Clip", 255;
		DropItem "MetaMachineGun", 32;
	}

	States
	{
	Spawn:
		MGMR A 4 A_Look;
		Loop;
	Missile:
		MGMR E 10 A_FaceTarget;
		MGMR FE 4 BRIGHT 
		{
			A_PlaySound ("weapons/machinegun/fire");
			A_SpawnProjectile("MonsterZombiemanTracer",32,0,random(-4,4),CMF_OFFSETPITCH,random(-4,4));
		}
		MGMR F 1 A_CPosRefire;
		Goto Missile+1;
	}
}


// Rocket Launcher marine. Probably horrible to fight.

class MetaMarineRocket : MetaScriptedMarine // replaces MarineRocket
{
	Default
	{
		Obituary "$OB_MARINE_ROCKET";
		DropItem "RocketAmmo", 255;
		DropItem "MetaRocketLauncher", 32;
	}

	States
	{
	Spawn:
		PLAR A 4 A_Look;
		Loop;
	Missile:
		PLAR E 10 A_FaceTarget;
		PLAR F 8 
		{
			A_SpawnProjectile("MetaLauncherRocket");
			A_PlaySound("weapons/rocketlauncher/fire", CHAN_WEAPON);
		}
		PLAR E 8;
		Goto See;
	}

}

// Plasma Rifle marine. Bloop bloop bloop.

class MetaMarinePlasma : MetaScriptedMarine // replaces MarinePlasma
{
	Default
	{
		Obituary "$OB_MARINE_PLASMA";
		DropItem "Cell", 255;
		DropItem "MetaPlasmaRifle", 32;
	}

	States
	{
	Spawn:
		PLPR A 4 A_Look;
		Loop;
	Missile:
		PLPR E 10 A_FaceTarget;
		PLPR FE 4 BRIGHT 
		{
			A_PlaySound ("weapons/plasmarifle/fire");
			A_SpawnProjectile("MetaPlasmaBall");
		}
		PLPR F 1 A_CPosRefire;
		Goto Missile+1;
	}

}

// Gauss Cannon marine. I still don't know why ZDoom pushed the Railgun so hard
// back in the day, but sure, alright.

class MetaMarineRailgun : MetaScriptedMarine // replaces MarineRailgun
{
	Default
	{
		Obituary "$OB_MARINE_GAUSS";
		DropItem "Cell", 255;
		DropItem "MetaGaussCannon", 32;
	}

	States
	{
	Spawn:
		PPUB A 4 A_Look;
		Loop;
	Missile:
		PPUB E 10 A_FaceTarget;
		PPUB E 4;
		PPUB F 8 
		{
			A_CustomRailgun ((200), 0, "Blue", "White", RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING, 0, 0, "GaussExplosion");
			A_PlaySound ("weapons/gauss/fire", CHAN_WEAPON);
		}
		PPUB E 8;
		Goto See;
	}

}

// BFG Marine. Anyone who forces you to fight this is a bum.

class MetaMarineBFG : MetaScriptedMarine // replaces MarineBFG
{
	Default
	{
		Obituary "$OB_MARINE_BFG";
		DropItem "BFGAmmo", 255;
		DropItem "MetaBFG9000", 32;
	}

	States
	{
	Spawn:
		PLBF A 4 A_Look;
		Loop;
	Missile:
		PLBF E 5 { A_FaceTarget(); A_PlaySound("weapons/bfg/charge"); }
		PLBF EEEEE 5 A_FaceTarget;
		PLBF F 6 BRIGHT 
		{
			A_PlaySound ("weapons/bfg/fire1");
			
			// stops the marine from hurting himself with bfg tracers if he isn't friendly
			if (bFRIENDLY == 1)
			{
				A_SpawnProjectile("MetaBFGBall");
			} else {
				A_SpawnProjectile("EvilMarineMetaBFGBall");
			}
		}
		PLBF A 4 A_FaceTarget;
		goto See;
	}
}

class EvilMarineMetaBFGBall : MetaBFGBall
{
	States
	{
	Roam:
		BFS1 AAAABBBB 1 Bright 
		{
			A_SetScale(frandom(0.8,1.2),frandom(0.8,1.2));
			A_SpawnItemEx("BFGTrail", frandom(-10,10), frandom(-10,10), frandom(0,20), frandom(-2.5,2.5), frandom(-2.5,2.5), frandom(-2.5,2.5), 0, SXF_CLIENTSIDE, 0);
		}
		BFS1 A 0
		{
			A_SpawnProjectile("EvilMarineBFGZapper", 8, 0, 0, CMF_AIMDIRECTION | CMF_TRACKOWNER);
		}
		Loop;
	}
}


class EvilMarineBFGZapper : BFGZapper
{
	Default
	{
		-FRIENDLY
	}
}