ACTOR MetaRevenant : MonsterCore
{
	Health 300
	Radius 20
	Height 56
	Mass 500
	Speed 10
	PainChance 100
	Monster
	MeleeThreshold 196
	+MISSILEMORE 
	+FLOORCLIP
	SeeSound "monsters/revenant/see"
	PainSound "monsters/revenant/pain"
	DeathSound "monsters/revenant/death"
	ActiveSound "monsters/revenant/idle"
	MeleeSound "skeleton/melee"
	HitObituary "$OB_UNDEADHIT"
	Obituary "$OB_UNDEAD"
	States
	{
	Spawn:
		SKEL AB 10 A_Look
		Loop
	See:
		"####" A 0 A_MetaBleed("FlyingBlood")
		SKEL AABBCCDDEEFF 2 A_Chase
		Loop
	Melee:
		SKEL G 0 A_FaceTarget
		SKEL G 6 { A_PlaySound("monsters/revenant/wiff"); A_FaceTarget; }
		SKEL H 6 A_FaceTarget
		SKEL I 6 A_CustomMeleeAttack(random(1, 10) * 6, "monsters/revenant/hit")
		Goto See
	Missile:
		SKEL J 0 Bright A_FaceTarget
		SKEL J 10 Bright A_FaceTarget
		SKEL K 10 A_SkelMissile
		SKEL K 10 A_FaceTarget
		Goto See
	Pain:
		SKEL L 5
		SKEL L 5 A_Pain
		Goto See
	Death:
		SKEL L 0 A_Jump (128, 2)
		SKEL L 0 A_SetScale(-1,1) // random chance of sprite flipping
		TNT1 A 0 A_SpawnItem("SoulSpawner")
		SKEL L 7 A_GiveToTarget("ScoreItem", 1200)
		SKEL M 7 A_PlaySound("monsters/revenant/boned")
		SKEL N 7 A_Scream
		SKEL O 7 A_NoBlocking
		SKEL P 7 A_PlaySound("bodyfalls/revenant")
		SKEL Q -1
		Stop
	Raise:
		SKEL Q 5 A_SetScale(1,1)
		SKEL PONML 5
		Goto See
	}
}

ACTOR MetaRevenantTracer replaces RevenantTracer
{
	Radius 11
	Height 8
	Speed 10
	Damage 10
	Projectile
	+SEEKERMISSILE
	+RANDOMIZE
	SeeSound "monsters/revenant/fire"
	//DeathSound "skeleton/tracex"
	decal RevenantScorch
	RenderStyle Add
	States
	{
	Spawn:
		FATB A 0
		FATB A 0
		FATB A 0 A_JumpIfInventory("ExtinguisherToken",1,"Extinguished")
		FATB A 2 Bright A_Tracer
		FATB A 0 A_JumpIfInventory("ExtinguisherToken",1,"Extinguished")
		FATB B 2 Bright A_Tracer
		Loop
	Death:
		TNT1 A 0
		TNT1 A 0 A_SpawnItem("MetaRevenantTracerExplosion")
		Stop
	Extinguished:
		BAL1 A 0 A_PlaySound("monsters/lostsoul/extinguish")
		TNT1 AAAAA 1 A_SpawnItem ("DoomImpBallEffect", 0, 0, 0, 0)
		stop
	}
}

ACTOR MetaRevenantTracerSmoke replaces RevenantTracerSmoke
{
  +NOBLOCKMAP
  +NOGRAVITY
  states
  {
  Spawn:
	PUF2 A 0 
	PUF2 A 0 A_PlaySound("monsters/revenant/missile/beep")
    PUF2 ABC 2 A_FadeOut
  Finalize:
    PUF2 D 2 A_FadeOut
   loop
 }
}

ACTOR MetaRevenantTracerExplosion : Explosion
{
	//scale 0.5
	States
	{
		Spawn:
		FBXP A 0
		FBXP A 2 BRIGHT
		{
			A_PlaySound("weapons/shotgun/grenade", CHAN_WEAPON);
			A_Quake (3,5,0,750,0);
			A_SpawnItem("ShockwaveSmall");
		}
		FBXP B 2 BRIGHT 
		FBXP C 2 BRIGHT
		Stop
	}
}