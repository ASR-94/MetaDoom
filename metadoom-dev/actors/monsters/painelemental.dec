ACTOR MetaPainElemental : MonsterCore
{
	Health 400
	Radius 31
	Height 56
	Mass 400
	Speed 8
	PainChance 128
	Monster
	+FLOAT 
	+NOGRAVITY
	+FLOATBOB
	SeeSound "monsters/painelemental/see"
	PainSound "monsters/painelemental/pain"
	DeathSound "monsters/painelemental/die"
	ActiveSound "monsters/painelemental/idle"
	Obituary "$OB_PAINELEMENTAL"
	States
	{
	Spawn:
		PAIN A 10 A_Look
		Loop
	See:
		"####" A 0
		{
			if (CountInv("BleedingCounter") > 0)
			{
				A_DamageSelf ((5), "Bleeding");
				A_TakeInventory("BleedingCounter", 1);
				A_CustomMissile ("FlyingBlood", 0, 0, random (0, 360), 2, random (0, 160));
				A_PlaySound("monsters/bleed", CHAN_5);
			}
		}
		PAIN AABBCC 3 A_Chase
		Loop
	Scurred:
		PAIN A 0 A_ChangeFlag("FRIGHTENED", TRUE)
	ScurredLoop:
		PAIN A 0 A_GiveInventory("ScurredCounter", 1)
		PAIN A 2 A_Chase("", "")
		PAIN A 2 A_Chase("", "")
		PAIN A 0 A_SpawnItem("HolySweat",0,54)
		PAIN B 2 A_Chase("", "")
		PAIN B 2 A_Chase("", "")
		PAIN C 2 A_Chase("", "")
		PAIN C 2 A_Chase("", "")
		PAIN C 0 A_SpawnItem("HolySweat",0,54)
		PAIN B 2 A_Chase("", "")
		PAIN B 2 A_Chase("", "")
		PAIN B 0 A_JumpIfInventory("ScurredCounter", 6, "ScurredEnd")
		loop
	ScurredEnd:
		POSS D 0
		{
			A_ChangeFlag("FRIGHTENED", FALSE);
			A_TakeInventory("ScurredCounter",999);
		}
		goto See
	Missile:
		PAIN D 5 A_FaceTarget
		PAIN E 5 A_FaceTarget
		PAIN F 5 BRIGHT A_FaceTarget
		PAIN F 0 BRIGHT A_PainAttack("MetaLostSoul")
		Goto See
	Melee:
		PAIN D 4 A_FaceTarget
		PAIN E 4 A_FaceTarget
		PAIN F 4 BRIGHT A_FaceTarget
		PAIN F 0 BRIGHT A_CustomMeleeAttack(10, "monsters/painelemental/bite", "monsters/painelemental/bite", PainElementalBite, 1)
		PAIN E 4 A_FaceTarget
		Goto See
	Pain:
		PAIN G 6
		PAIN G 6 A_Pain
		Goto See
	Pain.HolyWater:
		PAIN G 3
		PAIN G 3 A_Pain
		Goto Scurred
	Death:
		PAIN H 0 A_GiveToTarget("ScoreItem", 500)
		PAIN H 8 BRIGHT A_ChangeFlag("FLOATBOB", FALSE)
		PAIN I 8 BRIGHT A_Scream
		PAIN JK 8 BRIGHT 
		PAIN L 8 BRIGHT A_PainDie("MetaLostSoul")
		PAIN M 8 BRIGHT
		Stop
	Raise:
		PAIN MLKJIH 8
		Goto See
	}
}