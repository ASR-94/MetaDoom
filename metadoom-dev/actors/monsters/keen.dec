/*
----------------------------
MetaDoom
Actor Definitions
Optional Content
----------------------------
*/

actor KeenSpawner // replaces CommanderKeen
{
	Health 0x7fffffff
	Height 0
	Radius 0
	MONSTER
	-COUNTKILL
	-SOLID
	+NOCLIP
	+NOTDMATCH
	+SPAWNCEILING 
	States
	{
	Spawn:
		KEEN A 0
		KEEN A 0 A_JumpIf(GetCvar(meta_noalts_actors) == 1, "Keen")
		KEEN A 0 A_Jump(256,"Keen","Budo","Dave","Hovertank","Rover","Catacomb")
	Keen:
		TNT1 A 0 A_SpawnItemEx("MetaKeen",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION|SXF_TRANSFERAMBUSHFLAG)
		goto CreepyStalker
	Budo:
		TNT1 A 0 A_SpawnItemEx("MetaBudo",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION|SXF_TRANSFERAMBUSHFLAG)
		goto CreepyStalker
	Dave:
		TNT1 A 0 A_SpawnItemEx("MetaDave",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION|SXF_TRANSFERAMBUSHFLAG)
		goto CreepyStalker
	Hovertank:
		TNT1 A 0 A_SpawnItemEx("MetaHovertank",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION|SXF_TRANSFERAMBUSHFLAG)
		goto CreepyStalker
	Rover:
		TNT1 A 0 A_SpawnItemEx("MetaRover",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION|SXF_TRANSFERAMBUSHFLAG)
		goto CreepyStalker
	Catacomb:
		TNT1 A 0 A_SpawnItemEx("MetaCatacomb",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION|SXF_TRANSFERAMBUSHFLAG)
		goto CreepyStalker
	CreepyStalker:
		TNT1 A 1 // SFX: HEAVY BREATHING
		loop
	Death:
		TNT1 A 1
		TNT1 A 1 A_KeenDie
		TNT1 A -1
		Stop
	}
}

// Commander Keen - Commander Keen Episode IV: Goodbye Galaxy!
// Basically the same as in vanilla, but with Adlib Keen 4 sounds instead of Keen1 ones.
ACTOR MetaKeen
{
	Health 100
	Radius 16
	Height 72
	Mass 10000000
	PainChance 256
	+SOLID 
	+SPAWNCEILING 
	+NOGRAVITY 
	+SHOOTABLE 
	+COUNTKILL 
	+NOICEDEATH
	+ISMONSTER
	+DONTFALL
	PainSound "keens/keen/hit"
	DeathSound "keens/keen/die"
	damagefactor HolyWater, 0
	damagefactor FireExtinguisher, 0
	damagefactor DogCollar, 0
	painchance DogCollar, 0
	States
	{
	Spawn:
		KEEN A   -1
		Loop
	Death:
		KEEN Z 0 A_GiveToTarget("ScoreItem", 666)
		KEEN Z 0 A_Scream
		KEEN Z 0 A_Fall
		KEEN Z 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		KEEN Z	-1 A_KillMaster //A_KeenDie
		Stop
	Pain:
		KEEN M	4
		KEEN M	8 A_Pain
		Goto Spawn
	}
}

// Player - Shadow Knights: The Shogun of Death
// (or "Budo: The Art of Ninja Combat!" in the budget re-release.)
// Id Software's first official game, a rough and unrefined Ninja-themed platformer.
ACTOR MetaBudo : MetaKeen
{
	PainSound "keens/budo/hit"
	DeathSound "keens/budo/die"
	States
	{
	Spawn:
		KBUD A -1
		Loop
	Pain:
		KBUD B	4
		KBUD B	8 A_Pain
		Goto Spawn
	}
}

// Dave - Dangerous Dave in the Haunted Mansion
// Another Softdisk platformer, with the best shotgun ever witnessed in a
// 2D platformer (non-Metal Slug division).
ACTOR MetaDave : MetaKeen
{
	PainSound "keens/dave/hit"
	DeathSound "keens/dave/die"
	States
	{
	Spawn:
		KDAV A -1
		Loop
	Pain:
		KDAV B	4
		KDAV B	8 A_Pain
		Goto Spawn
	}
}

// Hostage - Hovertank 3-D
// Id's first FPS! Now we're getting somewhere. This used a primitive ancestor
// to the Wolf3D engine using flatshaded walls.
ACTOR MetaHovertank : MetaKeen
{
	PainSound "keens/hovertank/hit"
	DeathSound "keens/hovertank/die"
	States
	{
	Spawn:
		KHOV AB 8
		Loop
	Pain:
		KHOV C	4
		KHOV C	8 A_Pain
		Goto Spawn
	}
}

// Rover - Rescue Rover!
// A curious dog-rescuing puzzle game I've never really been able to get into.
ACTOR MetaRover : MetaKeen
{
	PainSound "keens/rover/hit"
	DeathSound "keens/rover/die"
	States
	{
	Spawn:
		KROV A -1
		Loop
	Pain:
		KROV B	4
		KROV B	8 A_Pain
		Goto Spawn
	}
}

// Nemesis - Catacomb 3D
// Another Wolf ancestor, this series has some interesting features like
// destroyable walls and lore scrolls and really disappointing endings.
ACTOR MetaCatacomb : MetaKeen
{
	PainSound "keens/catacomb/hit"
	DeathSound "keens/catacomb/die"
	States
	{
	Spawn:
		KCAT AB 4
		Loop
	Pain:
		KCAT C	4
		KCAT C	8 A_Pain
		Goto Spawn
	}
}