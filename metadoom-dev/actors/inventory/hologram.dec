// Grenades
actor HologramPickup : CustomInventory
{
	inventory.pickupmessage "$GOTHOLOGRAM"
	Tag "$TAGHOLOGRAM"
	inventory.icon "INVHOLO"
	inventory.pickupsound "inventory/hologram/get"
	+INVENTORY.ALWAYSPICKUP
	scale 0.5
	states
	{
	Spawn:
		HOLO Z -1
		stop
	Pickup:
		TNT1 A 0 A_GiveInventory ("Hologram", 1)
		TNT1 A 0 A_GiveInventory ("GrenadeAmmo", 3)
		stop
	}
}

actor Hologram : CustomInventory
{
	inventory.pickupmessage "$GOTHOLOGRAM"
	Tag "$TAGHOLOGRAM"
	inventory.icon "INVHOLO"
	inventory.pickupsound "inventory/hologram/get"
	inventory.amount 1
	inventory.maxamount 1
	+INVENTORY.INVBAR
	scale 0.5
	states
	{
	Spawn:
		HOLO Z -1
		stop
	Use:
		TNT1 A 0 A_JumpIfInventory("GrenadeAmmo", 1, "Fire")
		goto Failure
	Fire:
		TNT1 A 0 A_FireCustomMissile("HologramProj",0,0,0,0,FPF_TRANSFERTRANSLATION) //A_SpawnItemEx("HologramSam",32,0,8,0,0,0,0,SXF_TRANSFERTRANSLATION) //A_FireCustomMissile("HologramProj",0,0)
		TNT1 A 0 A_TakeInventory("GrenadeAmmo", 1)
		fail
	Failure:
		TNT1 A 0 A_PlaySound("menu/invalid")
		TNT1 A 0 A_Log("You don't have any grenades!")
		fail
	}
}

actor HologramProj
{
	+CANBOUNCEWATER
	Radius 8
	Height 8
	Speed 5
	Damage 0
	PROJECTILE
	+RANDOMIZE
	-NOGRAVITY
	+USEBOUNCESTATE
	+ROLLSPRITE
	BounceType "Doom"
	SeeSound "inventory/hologram/toss"
	BounceSound "inventory/grenade/bounce"
	-BOUNCEONFLOORS
	scale 0.3
	ReactionTime 60	//Just over five seconds
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ThrustThingZ(0,32,0,1)
		TNT1 A 0 A_PlaySound("weapons/grenadethrow")
		goto Flying
	Flying:
		HOLO Y 3 A_CountDown
		loop
	Death:
		TNT1 A 0 A_SpawnItemEx("HologramSam",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		Stop
	}
}

ACTOR HologramSam			// you're my main man!
{
	Radius 20
	Height 56
	Speed 8
	+FLOORCLIP
	+SHOOTABLE
	+FRIENDLY
	+INVULNERABLE
	+NODAMAGE
	+NOBLOCKMAP
	-SOLID
	+NONSHOOTABLE
	var int user_seconds;
	States
	{
	Spawn:
		PLAY A 0 
		PLAY A 0 A_PlaySound("inventory/hologram/on")
		PLAY A 0 Thing_ChangeTID (0, 3333)
		PLAY A 0 A_RadiusGive ("ForgetAboutDre", 2048, RGF_MONSTERS, 1)
		PLAY A 1 A_SetScale (0.1)
		PLAY A 1 A_SetScale (0.3,0.2)
		PLAY A 1 A_SetScale (0.4,0.5)
		PLAY A 1 A_SetScale (0.7,0.9)
		PLAY A 1 A_SetScale (1.2, 1.1)
		PLAY A 1 A_SetScale (1,1)
		goto Active
	Active:
		PLAY A 0 A_AlertMonsters(0, AMF_TARGETEMITTER)
		PLAY AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SetTranslucent(frandom(0.5,1.0),0)
		PLAY A 0 A_SetUserVar(user_seconds, (user_seconds + 1))
		PLAY A 0 A_JumpIf(user_seconds >= 10, "End")
		Loop
	End:
		PLAY A 0 A_PlaySound("inventory/hologram/off")
		PLAY A 1 A_SetScale (0.9,1)
		PLAY A 1 A_SetScale (0.8,1)
		PLAY A 1 A_SetScale (0.75,1)
		PLAY A 1 A_SetScale (0.7,1)
		PLAY A 1 A_SetScale (0.6,1)
		PLAY A 1 A_SetScale (0.4,1)
		PLAY A 1 A_SetScale (0.1,1)
		PLAY A 1 A_SetScale (0.02,1)
		Stop
	}
}

actor ForgetAboutDre : CustomInventory
{
	states
	{
	Pickup:
		//TNT1 A 0 A_ClearTarget
		TNT1 A 0 Thing_Hate (0, 3333, 0)
		stop
	}
}