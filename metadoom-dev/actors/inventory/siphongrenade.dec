// Siphon Grenades, the bane of balance
actor SiphonGrenadePickup : CustomInventory
{
	inventory.pickupmessage "$GOTSIPHON"
	Tag "$TAGSIPHON"
	inventory.icon "INVSIPH"
	inventory.pickupsound "inventory/grenade/get"
	+INVENTORY.ALWAYSPICKUP
	scale 0.5
	states
	{
	Spawn:
		SIPH Z -1
		stop
	Pickup:
		TNT1 A 0 A_GiveInventory ("SiphonGrenade", 1)
		TNT1 A 0 A_GiveInventory ("GrenadeAmmo", 3)
		stop
	}
}

actor SiphonGrenade : CustomInventory
{
	inventory.pickupmessage "$GOTSIPHON"
	Tag "$TAGSIPHON"
	inventory.icon "INVSIPH"
	inventory.pickupsound "inventory/grenade/get"
	inventory.amount 1
	inventory.maxamount 1
	+INVENTORY.INVBAR
	scale 0.5
	states
	{
	Spawn:
		SIPH Z -1
		stop
	Use:
		TNT1 A 0 A_JumpIfInventory("InventoryCooldownToken", 1, "FailureCooldown")
		TNT1 A 0 ACS_NamedExecuteWithResult("InventoryCooldown")
		TNT1 A 0 A_JumpIfInventory("GrenadeAmmo", 1, "Fire")
		goto Failure
	Fire:
		TNT1 A 0 A_FireCustomMissile("SiphonGrenadeProj",0,0)
		TNT1 A 0 A_TakeInventory("GrenadeAmmo", 1)
		fail
	Failure:
		TNT1 A 0 A_PlaySound("menu/invalid")
		TNT1 A 0 A_Log("You don't have any grenades!")
		fail
	FailureCooldown:
		TNT1 A 0 
		TNT1 A 0 
		fail
	}
}

actor SiphonGrenadeProj
{
	+CANBOUNCEWATER
	Radius 8
	Height 8
	Speed 20
	Damage 0
	PROJECTILE
	+RANDOMIZE
	-NOGRAVITY
	//+USEBOUNCESTATE
	+ROLLSPRITE
	+FORCERADIUSDMG
	//BounceType "Doom"
	SeeSound "inventory/throw"
	//BounceSound "inventory/grenade/bounce"
	Obituary "$OB_SIPHON"
	//scale 0.5
	damagetype Vampire
	ReactionTime 60	//Just over five seconds
	States
	{
	Spawn:
		SIPH A 0
		SIPH A 0
		{
			ThrustThingZ(0,32,0,1);
			A_PlaySound("weapons/grenadethrow");
		}
		goto Bounce
	Bounce:
		"####" # 0 A_Jump(128, "Bounce2")
		goto Bounce1
	Bounce1:
		"####" # 0 A_CountDown
		"####" # 1 A_SetRoll((roll + 5))
		"####" # 1 A_SetRoll((roll + 5))
		"####" # 1 A_SetRoll((roll + 5))
		loop
	Bounce2:
		"####" # 0 A_CountDown
		"####" # 1 A_SetRoll((roll - 5))
		"####" # 1 A_SetRoll((roll - 5))
		"####" # 1 A_SetRoll((roll - 5))
		loop
	Death:
		TNT1 A 0 A_PlaySound("inventory/siphongrenade/explode")
		TNT1 A 0 A_SpawnItem("SiphonExplosionFX")
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAA 2 A_Explode((8), 100, !XF_HURTSOURCE)
		Stop
	}
}

ACTOR SiphonExplosionFX
{
	Radius 10
	Height 10
	+NOBLOCKMAP
	-SOLID
	+NOGRAVITY
	+FORCEXYBILLBOARD
	+ROLLSPRITE
	renderstyle Add
	States
	{
		Spawn:
			EXPL A 0
			SIBM ABABABABABABABABABABABABABABABABABABABABABABABABABABABABAB 1 bright 
			{
				A_SetScale(frandom(0.9,1.1),frandom(0.9,1.1));
				A_SetTranslucent(frandom(0.9,0.8), 1);
				A_SetRoll(random(0,360));
			}
		Die:
			SIBM AB 1 bright A_FadeOut(0.25)
			loop
	}
}