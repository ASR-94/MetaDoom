// Kinetic Mine
actor KineticMinePickup : CustomInventory
{
	inventory.pickupmessage "$GOTKINMINE"
	Tag "$TAGKINMINE"
	inventory.icon "INVMINE"
	inventory.pickupsound "inventory/kineticmine/get"
	+INVENTORY.ALWAYSPICKUP
	scale 0.75
	states
	{
	Spawn:
		KMIN Z -1
		stop
	Pickup:
		TNT1 A 0 A_GiveInventory ("KineticMine", 1)
		TNT1 A 0 A_GiveInventory ("GrenadeAmmo", 3)
		stop
	}
}

actor KineticMinePickupRetrieve : KineticMinePickup
{
	inventory.pickupmessage "$GOTKINMINERET"
	states
	{
	Pickup:
		TNT1 A 0 A_GiveInventory ("KineticMine", 1)
		TNT1 A 0 A_GiveInventory ("GrenadeAmmo", 1)
		stop
	}
}

actor KineticMine : CustomInventory
{
	inventory.pickupmessage "$GOTKINMINE"
	Tag "$TAGKINMINE"
	inventory.icon "INVMINE"
	inventory.pickupsound "inventory/kineticmine/get"
	inventory.amount 1
	inventory.maxamount 1
	+INVENTORY.INVBAR
	scale 0.5
	states
	{
	Spawn:
		KMIN Z -1
		stop
	Use:
		TNT1 A 0 A_JumpIfInventory("InventoryCooldownToken", 1, "FailureCooldown")
		TNT1 A 0 ACS_NamedExecuteWithResult("InventoryCooldown")
		TNT1 A 0 A_JumpIfInventory("GrenadeAmmo", 1, "Fire")
		goto Failure
	Fire:
		TNT1 A 0 A_FireCustomMissile("KineticMineProj",0,0,0,0)
		TNT1 A 0 A_TakeInventory("GrenadeAmmo", 1)
		fail
	Failure:
		TNT1 A 0 A_PlaySound("menu/invalid")
		TNT1 A 0 A_Log("You don't have any grenades!")
		fail
	FailureCooldown:
		TNT1 A 0 
		TNT1 A 0 
		fail
	}
}

/*
actor KineticMineProj
{
	+CANBOUNCEWATER
	Radius 8
	Height 8
	Speed 10
	Damage 0
	PROJECTILE
	+RANDOMIZE
	-NOGRAVITY
	+USEBOUNCESTATE
	+CORPSE
	BounceType "Doom"
	SeeSound "inventory/throw"
	-BOUNCEONFLOORS
	scale 0.5
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ThrustThingZ(0,32,0,1)
		TNT1 A 0 A_PlaySound("weapons/grenadethrow")
		goto Flying
	Flying:
		HOLO Y 3 
		loop
	Bounce.Wall:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("KineticMineLurkingWall",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		Stop
	Death:
		TNT1 A 0 A_SpawnItemEx("KineticMineLurkingFloor",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		Stop
	}
}
*/

ACTOR KineticMineBeepyBoop
{
	+NOBLOCKMAP
	+NOGRAVITY
	Radius 1
	Height 1
	scale 0.5
	states
	{
	Spawn:
		KMIN Y 1 BRIGHT A_FadeOut
		loop
	}
}

ACTOR KineticMineLurkingFloor
{
	Radius 8
	Height 8
	scale 0.5
	+LOOKALLAROUND
	+FRIENDLY
	
	var int user_fuse;
	
	States
	{
	Spawn:
		KMIN B 0
		KMIN B 0 A_PlaySound("inventory/kineticmine/land")
		goto WaitMyLove
	WaitMyLove:
		"####" "#" 0 { user_fuse++; }
		"####" "#" 0 A_JumpIf(user_fuse == 20, "Detatch")
		"####" "#" 0 A_SpawnItemEx("KineticMineBeepyBoop",0,0,8)
		"####" "##" 10 A_LookEx (LOF_NOSOUNDCHECK, 0, 256, 0, 360)
		Loop
	See:
		"####" "#" 0 A_PlaySound("inventory/kineticmine/track")
		"####" "#" 2 Bright 
		"####" "#" 2
		"####" "#" 0 A_PlaySound("inventory/kineticmine/track")
		"####" "#" 2 Bright 
		"####" "#" 2
		"####" "#" 0 A_PlaySound("inventory/kineticmine/track")
		"####" "#" 2 Bright 
		"####" "#" 2
		"####" "#" 0 A_PlaySound("inventory/kineticmine/track")
		"####" "#" 2 Bright 
		"####" "#" 2
		"####" "#" 0 A_PlaySound("inventory/kineticmine/track")
		"####" "#" 2 Bright 
		"####" "#" 2
		"####" "#" 0 A_Jump(256, "Kaboom")
		goto Kaboom
	Kaboom:
		"####" "#" 0
		{
			A_FaceTarget;
			A_PlaySound("inventory/kineticmine/launch");
			A_SpawnProjectile("KineticMineLeapy",4);
		}
		Stop
	Detatch:
		TNT1 A 0 A_SpawnItem("KineticMinePickupRetrieve")
		Stop
	}
}

ACTOR KineticMineLurkingCeiling : KineticMineLurkingFloor
{
	Radius 8
	Height 4
	scale -0.5
	+LOOKALLAROUND
	+FRIENDLY
	+NOGRAVITY
	States
	{
	Spawn:
		KMIN B 0
		KMIN B 0 A_PlaySound("inventory/kineticmine/land")
		goto WaitMyLove
	WaitMyLove:
		"####" "#" 0 { user_fuse++; }
		"####" "#" 0 A_JumpIf(user_fuse == 20, "Detatch")
		"####" "#" 0 A_SpawnItemEx("KineticMineBeepyBoop",0,0,-8)
		"####" "##" 10 A_LookEx (LOF_NOSOUNDCHECK, 0, 256, 0, 360)
		Loop
	Kaboom:
		"####" "#" 0
		{
			A_FaceTarget;
			A_PlaySound("inventory/kineticmine/launch");
			A_SpawnProjectile("KineticMineLeapy",-4);
		}
		Stop
	}
}

ACTOR KineticMineLurkingWall : KineticMineLurkingFloor
{
	Radius 8
	Height 8
	scale 0.5
	+LOOKALLAROUND
	+FRIENDLY
	+WALLSPRITE
	+NOGRAVITY
	States
	{
	Spawn:
		KMIN C 0
		KMIN C 0 A_PlaySound("inventory/kineticmine/land")
		goto WaitMyLove
	WaitMyLove:
		"####" "#" 0 { user_fuse++; }
		"####" "#" 0 A_JumpIf(user_fuse == 20, "Detatch")
		"####" "#" 0 A_SpawnItemEx("KineticMineBeepyBoop",-4,0,0)
		"####" "##" 10 A_LookEx (LOF_NOSOUNDCHECK, 0, 256, 0, 360)
		Loop
	Kaboom:
		"####" "#" 0
		{
			A_SpawnItemEx("KineticMineLurkingWallWorkaround",-32,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		}
		Stop
	Detatch:
		"####" "#" 0 A_SpawnItemEx("KineticMinePickupRetrieve",-32,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		Stop
	}
}

// uggh, gross
ACTOR KineticMineLurkingWallWorkaround
{
	+NOGRAVITY
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0
		{
			A_FaceTarget;
			A_PlaySound("inventory/kineticmine/launch");
			A_SpawnProjectile("KineticMineLeapy",4);
		}
		Stop
	}
}

ACTOR KineticMineLeapy
{
	Radius 11
	Height 8
	Speed 20
	Damage 20
	Projectile
	+RANDOMIZE
	Obituary "$OB_KINMINE"
	Decal Scorch
	States
	{
	Spawn:
		KMIS A 0 A_SpawnItem ("RocketTrailSmoke", 0, 6, 0, 0)
		KMIS AA 1 Bright
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("Explosion",0,0,24,0,0,0,0,SXF_TRANSFERPOINTERS)
		Stop
	}
}