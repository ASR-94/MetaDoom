// Kinetic Mine
actor KineticMinePickup : CustomInventory
{
	inventory.pickupmessage "$GOTKINMINE"
	Tag "$TAGKINMINE"
	inventory.icon "INVMINE"
	inventory.pickupsound "inventory/kineticmine/get"
	+INVENTORY.ALWAYSPICKUP
	scale 0.5
	states
	{
	Spawn:
		KMIN Z -1
		stop
	Pickup:
		TNT1 A 0 A_GiveInventory ("KineticMine", 1)
		TNT1 A 0 A_GiveInventory ("GrenadeAmmo", 3)
		stop
	}
}

actor KineticMine : CustomInventory
{
	inventory.pickupmessage "$GOTKINMINE"
	Tag "$TAGKINMINE"
	inventory.icon "INVMINE"
	inventory.pickupsound "inventory/kineticmine/get"
	inventory.amount 1
	inventory.maxamount 1
	+INVENTORY.INVBAR
	scale 0.5
	states
	{
	Spawn:
		KMIN Z -1
		stop
	Use:
		TNT1 A 0 A_JumpIfInventory("InventoryCooldownToken", 1, "FailureCooldown")
		TNT1 A 0 ACS_NamedExecuteWithResult("InventoryCooldown")
		TNT1 A 0 A_JumpIfInventory("GrenadeAmmo", 1, "Fire")
		goto Failure
	Fire:
		TNT1 A 0 A_FireCustomMissile("KineticMineProj",0,0,0,0,FPF_TRANSFERTRANSLATION)
		TNT1 A 0 A_TakeInventory("GrenadeAmmo", 1)
		fail
	Failure:
		TNT1 A 0 A_PlaySound("menu/invalid")
		TNT1 A 0 A_Log("You don't have any grenades!")
		fail
	FailureCooldown:
		TNT1 A 0 
		TNT1 A 0 
		fail
	}
}

actor KineticMineProj
{
	+CANBOUNCEWATER
	Radius 8
	Height 8
	Speed 10
	Damage 0
	PROJECTILE
	+RANDOMIZE
	-NOGRAVITY
	+USEBOUNCESTATE
	-BOUNCEONFLOORS
	BounceType "Doom"
	SeeSound "inventory/throw"
	scale 0.5
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ThrustThingZ(0,32,0,1)
		TNT1 A 0 A_PlaySound("weapons/grenadethrow")
		goto Flying
	Flying:
		HOLO Y 3 
		loop
	Bounce.Wall:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("KineticMineLurkingWall",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		Stop
	Death:
		TNT1 A 0 A_SpawnItemEx("KineticMineLurkingFloor",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		Stop
	}
}

ACTOR KineticMineLurkingFloor
{
	Radius 8
	Height 8
	scale 0.5
	+LOOKALLAROUND
	+FRIENDLY
	States
	{
	Spawn:
		HOLO Y 0
		HOLO Y 0 A_PlaySound("inventory/kineticmine/land")
		goto WaitMyLove
	WaitMyLove:
		"####" "#" 10 A_LookEx (LOF_NOSOUNDCHECK, 0, 256, 0, 360)
		"####" "#" 10 Bright A_LookEx (LOF_NOSOUNDCHECK, 0, 256, 0, 360)
		Loop
	See:
		"####" "#" 0 A_PlaySound("inventory/kineticmine/track")
		"####" "#" 2 Bright
		"####" "#" 2
		"####" "#" 0 A_PlaySound("inventory/kineticmine/track")
		"####" "#" 2 Bright
		"####" "#" 2
		"####" "#" 0 A_PlaySound("inventory/kineticmine/track")
		"####" "#" 2 Bright
		"####" "#" 2
		"####" "#" 0 A_PlaySound("inventory/kineticmine/track")
		"####" "#" 2 Bright
		"####" "#" 2
		"####" "#" 0 A_PlaySound("inventory/kineticmine/track")
		"####" "#" 2 Bright
		"####" "#" 2
		goto Kaboom
	Kaboom:
		"####" "#" 0
		{
			A_FaceTarget;
			A_PlaySound("inventory/kineticmine/launch");
			A_CustomMissile("KineticMineLeapy",4);
		}
		Stop
	}
}

ACTOR KineticMineLurkingWall : KineticMineLurkingFloor
{
	Radius 8
	Height 8
	scale 0.5
	+LOOKALLAROUND
	+FRIENDLY
	+WALLSPRITE
	+NOGRAVITY
	States
	{
	Spawn:
		HGRE C 0
		HGRE C 0 A_PlaySound("inventory/kineticmine/land")
		goto WaitMyLove
	WaitMyLove:
		"####" "#" 10 A_LookEx (LOF_NOSOUNDCHECK, 0, 256, 0, 360)
		"####" "#" 10 Bright A_LookEx (LOF_NOSOUNDCHECK, 0, 256, 0, 360)
		Loop
	}
}

ACTOR KineticMineLeapy
{
	Radius 11
	Height 8
	Speed 20
	Damage 20
	Projectile
	+RANDOMIZE
	Obituary "$OB_KINMINE"
	Decal Scorch
	States
	{
	Spawn:
		MISL A 0 A_SpawnItem ("RocketTrailSmoke", 0, 6, 0, 0)
		MISL AA 1 Bright
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("Explosion",0,0,24,0,0,0,0,SXF_TRANSFERPOINTERS)
		Stop
	}
}