actor RLDetonator : _Boolean {}

ACTOR MetaRocketLauncherPickup : WeaponGiver
{
	DropItem "MetaRocketLauncher"
	Inventory.PickupMessage "$GOTLAUNCHER"
	Inventory.PickupSound "weapons/rocketlauncher/get"
	scale 0.75
	States
	{
	Spawn:
		RLAU Z 0
		RLAU Z 0 A_ChangeFlag ("DROPPED", 0)
		RLAU Z -1
		Stop
	}
}

ACTOR MetaRocketLauncher : MetaDoomWeapon
{
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "RocketAmmo"
	+WEAPON.NOAUTOFIRE
	Inventory.PickupMessage "$GOTLAUNCHER"
	Tag "$TAG_ROCKETLAUNCHER"
	Weapon.YAdjust 10
	Inventory.PickupSound "weapons/rocketlauncher/get"
	scale 0.75
	+WEAPON.NOALERT
	Weapon.BobRangeX 0.5
	Weapon.BobRangey 0.75
	States
	{
	Ready:
		RLAU A 1 A_WeaponReady
		"####" A 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch")
		Loop
	Deselect:
		RLAU A 1 
		RLAU A 0 A_JumpIf(health <= 0, "DeadDude")
		RLAU A 1 Offset(0,33) A_PlaySound("weapons/swap")
		RLAU A 1 Offset(0,35)
		RLAU A 1 Offset(0,39)
		RLAU A 1 Offset(0,44)
		RLAU A 1 Offset(0,49)
		RLAU A 1 Offset(0,55)
		RLAU A 1 Offset(0,60)
		RLAU A 1 Offset(0,70)
		RLAU A 1 Offset(0,80)
		RLAU A 1 Offset(0,90)
	FinishDeselect:
		RLAU A 0 A_Lower
		Loop
	Select:
		RLAU A 1 Offset(0,90) A_PlaySound("weapons/rocketlauncher/use")
		RLAU A 1 Offset(0,80)
		RLAU A 1 Offset(0,70)
		RLAU A 1 Offset(0,60)
		RLAU A 1 Offset(0,55)
		RLAU A 1 Offset(0,49)
		RLAU A 1 Offset(0,44)
		RLAU A 1 Offset(0,39)
		RLAU A 1 Offset(0,35)
		RLAU A 1 Offset(0,33)
		RLAU A 1 Offset(0,32)
		goto Ready
	Fire:
		RLAU B 0 A_GunFlash
		RLAU A 0 
		{
			A_FireCustomMissile ("MetaLauncherRocket");
			A_PlaySound ("weapons/rocketlauncher/fire", CHAN_WEAPON);
		
			if (CountInv("PowerDamage") > 0)
			{
				A_PlaySound("powerups/quad/use", CHAN_5);
			}
		
			A_GunFlash;
			A_AlertMonsters;
		}
		RLAU B 1 bright Offset(0,45) A_SetPitch(pitch-2)
		RLAU B 1 bright Offset(0,48) A_SetPitch(pitch-2)
		RLAU B 1 bright Offset(0,47)
		RLAU C 1 bright Offset(0,46) A_SetPitch(pitch+1)
		RLAU C 1 bright Offset(0,44) A_SetPitch(pitch+1)
		RLAU C 1 bright Offset(0,42) A_SetPitch(pitch+1)
		RLAU D 1 bright Offset(0,38) A_SetPitch(pitch+1) 
		RLAU D 1 bright Offset(0,36) A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY|WRF_NOBOB)
		RLAU D 1 Offset(0,34) A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY|WRF_NOBOB)
		RLAU A 1 Offset(0,33) A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY|WRF_NOBOB)
		RLAU A 1 Offset(0,32) A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY|WRF_NOBOB)
		RLAU A 8 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY|WRF_NOBOB)
		Goto Ready
	AltFire:
		RLAU A 0
		RLAU A 1 Offset(0,34)
		{
				A_GiveInventory("RLDetonator", 1);
				A_PlaySound("weapons/rocketlauncher/deto", CHAN_BODY);
		}
		RLAU A 1 Offset(0,33)
		RLAU A 1 Offset(0,32) A_TakeInventory("RLDetonator", 1)
		RLAU A 10
		goto Ready
	Flash:
		TNT1 A 3 Bright A_Light1
		TNT1 A 4 Bright
		TNT1 AA 4 Bright A_Light2
		Goto LightDone
	Spawn:
		RLAU Z -1
		Stop
	}
}

ACTOR MetaLauncherRocket
{
	Radius 11
	Height 8
	Speed 30
	Damage 20
	Projectile
	+RANDOMIZE
	Obituary "$OB_MPROCKET"
	Decal Scorch
	States
	{
	Spawn:
		MISL A 0 A_PlaySound ("weapons/rocketlauncher/loop", CHAN_BODY, 1.0, 1)
		MISL A 0 A_SpawnItem ("RocketTrailSmoke", 0, 6, 0, 0)
		MISL AAAA 1 Bright A_JumpIfInTargetInventory("RLDetonator",1,"Death")
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("Explosion",0,0,24,0,0,0,0,SXF_TRANSFERPOINTERS)
		Stop
	}
}