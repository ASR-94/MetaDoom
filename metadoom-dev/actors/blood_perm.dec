// Permanent version of the flat blood splat decal, to be spawned by props

Actor BloodSplat_Permanent
{
	var float user_scaleadd;
	var float user_z;
	Radius 1
	Height 1
	Projectile
	+NOINTERACTION
	+NOBLOCKMAP
	+NOTRIGGER
	+NOTELESTOMP
	+MOVEWITHSECTOR
	+RELATIVETOFLOOR
	+DONTSPLASH
	+FLATSPRITE
	+ROLLCENTER
	Renderstyle Shaded
	StencilColor "7b 00 00"
	Species "BloodSplat"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			angle = 				random(0,359);
			
			//This should only be needed once. MOVEWITHSECTOR and RELATIVETOFLOOR
			//should, in theory,  take care of everything else.
			A_Warp(AAPTR_DEFAULT,0,0,frandom(0.0100000, 0.0199999),0,WARPF_NOCHECKPOSITION|WARPF_TOFLOOR);
			A_RadiusGive("BloodReducer", GetCvar(meta_perf_bloodcullradius), RGF_MISSILES,1,None,"BloodSplat");
			
			user_z = z;
			
			//Ordinary splat?
			if (CheckClass("BloodSplat",AAPTR_DEFAULT) ||
				CheckClass("BloodSplatGreen",AAPTR_DEFAULT) ||
				CheckClass("BloodSplatBlue",AAPTR_DEFAULT))
			{
				return					A_Jump(256,"A","B","F");
			}
			
			//Small splat.
			return					A_Jump(256,"C","D","E","G");
		}
		
	A:	BSPL A 0 A_Jump(256,"WaitForever")
	B:	BSPL B 0 A_Jump(256,"WaitForever")
	C:	BSPL C 0 A_Jump(256,"WaitForever")
	D:	BSPL D 0 A_Jump(256,"WaitForever")
	E:	BSPL E 0 A_Jump(256,"WaitForever")
	F:	BSPL F 0 A_Jump(256,"WaitForever")
	G:	BSPL G 0 A_Jump(256,"WaitForever")
	W:	BSPL W 0 A_Jump(256,"WaitForever")
	X:	BSPL X 0 A_Jump(256,"WaitForever")
	
	WaitForever:
		"####" "#" 35
		{
			if (user_z != z)
			{
				A_Warp(AAPTR_DEFAULT,0,0,frandom(0.0100000, 0.0199999),0,WARPF_NOCHECKPOSITION|WARPF_TOFLOOR);
				user_z = z;
			}
		}
		Wait
	FadeWait:
		"####" "#" 24
		Wait
	}
}

// Nothing to adjust here! We already have the CheckClass above handling the
// state jumps. 
Actor BloodSplatSmall_Permanent : BloodSplat_Permanent
{ }

Actor BloodSplatGreen_Permanent : BloodSplat_Permanent
{ StencilColor "49 9a 3d"	}

Actor BloodSplatSmallGreen_Permanent : BloodSplatSmall_Permanent
{ StencilColor "49 9a 3d"	}

Actor BloodSplatBlue_Permanent : BloodSplat_Permanent
{ StencilColor "0 0 80"	}

Actor BloodSplatSmallBlue_Permanent : BloodSplatSmall_Permanent
{ StencilColor "0 0 80"	}